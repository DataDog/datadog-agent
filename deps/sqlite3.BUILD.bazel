"""Agent specific BUILD file for sqlite3."""

load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_shared_library.bzl", "cc_shared_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")

package(
    default_applicable_licenses = [":license"],
    default_visibility = ["//visibility:private"],
)

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:blessing"],
    visibility = ["//visibility:public"],
)

_SQLITE3_PUBLIC_HEADERS = [
    "sqlite3.h",
    "sqlite3ext.h",
]

cc_library(
    name = "sqlite3",
    srcs = [
        "sqlite3.c",
    ],
    hdrs = _SQLITE3_PUBLIC_HEADERS,
    copts = [
        "-I.",
        "-Wall",
    ] + select({
        "@platforms//os:windows": [
            "/O2",
            "/wd4232",  # nonstandard extension used
            "/wd4244",  # possible loss of data
            "/wd4267",  # possible loss of data
            "/wd4996",  # deprecated functions
        ],
        "@platforms//os:macos": [
            "-fPIC",
            "-O3",
            "-DSQLITE_ENABLE_FTS5",
            "-DSQLITE_ENABLE_RTREE",
            "-DSQLITE_ENABLE_JSON1",
        ],
        "//conditions:default": [
            "-fPIC",
            "-O3",
            "-DSQLITE_ENABLE_FTS5",
            "-DSQLITE_ENABLE_RTREE",
            "-DSQLITE_ENABLE_JSON1",
        ],
    }),
    linkopts = select({
        "@platforms//os:windows": [],
        "//conditions:default": ["-lpthread", "-ldl"],
    }),
    linkstatic = True,
    visibility = ["//visibility:public"],
)

cc_shared_library(
    name = "libsqlite3_so",
    shared_lib_name = select({
        "@platforms//os:windows": "sqlite3.dll",
        "@platforms//os:macos": "libsqlite3.dylib",
        "//conditions:default": "libsqlite3.so",
    }),
    deps = [":sqlite3"],
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "lib_files",
    srcs = [":libsqlite3_so"],
    prefix = "lib",
)

pkg_files(
    name = "hdr_files",
    srcs = _SQLITE3_PUBLIC_HEADERS,
    prefix = "include",
)

pkg_install(
    name = "install",
    srcs = [
        ":hdr_files",
        ":lib_files",
    ],
)
