"""Agent specific BUILD file for bzip2."""

load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_shared_library.bzl", "cc_shared_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")

package(
    default_applicable_licenses = [":license"],
    default_visibility = ["//packages:__subpackages__"],
)

license(
    name = "license",
    # TODO(aiuto): The omnibus script claimed this is a BSD-2-Clause license.
    # That is incorrect, but probably close enough from the point of view of
    # how we use it. For now we will go with a correct designation and worry
    # about the BSD declaration when we rewrite the license gathering code.
    # This is the current SPDX known version of the bzip2 license.
    license_kinds = ["@rules_license//licenses/spdx:bzip2-1.0.6"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

exports_files([
    "LICENSE",
])

VERSION = "1.0.8"

exports_files([
    "LICENSE",
])

# only for test cases in //deps/bzip
exports_files([
    "sample1.ref",
    "sample2.ref",
    "sample3.ref",
    "sample1.bz2",
    "sample2.bz2",
    "sample3.bz2",
])

_PUBLIC_HEADERS = [
    "bzlib.h",
]

cc_binary(
    name = "bzip2",
    srcs = ["bzip2.c"],
    deps = [
        ":libbz2",
    ],
    copts = [
        "-Wall",
        # -Winline is probably obsolete with modern compilers, but that is what
        # the upstream project uses.
        "-Winline",
        "-D_FILE_OFFSET_BITS=64",
    ],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "libbz2",
    srcs = [
        "blocksort.c",
        "bzlib.c",
        "bzlib_private.h",
        "compress.c",
        "crctable.c",
        "decompress.c",
        "huffman.c",
        "randtable.c",
    ],
    hdrs = _PUBLIC_HEADERS,
    copts = [
        "-Wall",
        "-D_FILE_OFFSET_BITS=64",
    ] + select(
        {
            #"@platforms//os:aix": [
            #    "-qpic=small",
            #    "-O2",
            #    "-D_ALL_SOURCE",
            #    "-D_LARGE_FILES",
            #],
            "@platforms//os:linux": [
                "-fPIC",
                "-O3",
            ],
            "@platforms//os:macos": [
                "-fpic",
                "-fPIC",
                "-O3",
            ],
            "@platforms//os:windows": [
                "-O2",
            ],
        },
        no_match_error = "No target platform detected",
    ),
    local_defines = select({
        "@platforms//os:windows": ["_CRT_NONSTDC_NO_WARNINGS"],
        "//conditions:default": [],
    }),
    includes = ["."],
    visibility = ["//visibility:public"],
)

cc_shared_library(
    name = "bz2",
    win_def_file = "libbz2.def",
    deps = [":libbz2"],
    user_link_flags = select({
        #"//bazel/platforms:os_aix": [
        #    "-qmkshrobj",
        #    "-Wl,-soname",
        #],
        #"@platforms//os:linux": [
        #     "-Wl,'@rpath/libbz2.so.1.0.8'",
        #],
        "@platforms//os:macos": [
            "-shared",
            # "-Wl,-install_name",
            # "-Wl,'@rpath/libbz2.so.1.0'",
        ],
        "@platforms//os:windows": [],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

so_symlink(
    name = "lib_files",
    src = ":bz2",
    libname = "libbz2",
    version = VERSION,
    prefix = "embedded",
)

pkg_files(
    name = "hdr_files",
    srcs = _PUBLIC_HEADERS,
    prefix = "embedded/include",
)

pkg_filegroup(
    name = "all_files",
    srcs = [
        ":hdr_files",
        ":lib_files",
    ],
    visibility = ["@@//deps/openscap:__pkg__", "@@//packages:__subpackages__"],
)

pkg_install(
    name = "install",
    srcs = [
        ":all_files",
    ],
)
