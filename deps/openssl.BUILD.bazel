load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all",
    srcs = glob(["**"]),
)

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_prefix = select({
        "@platforms//os:windows": "perl",
        "//conditions:default": "",
    }),
    configure_in_place = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        "@platforms//os:windows": "Configure",
    }),
    configure_options = select({
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
        ],
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
        ],
        "@platforms//os:windows": [
            "mingw64",
        ],
        "//conditions:default": [],
    }) + [
        "no-docs",
        "no-idea",
        "no-mdc2",
        "no-rc5",
        "shared",
        "no-ssl3",
        "no-gost",
        # Probably not needed if bazel can copy the file wherever it is
        "--libdir=lib",
        "zlib",
    ],
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all",
    out_shared_libs = select({
        "@platforms//os:linux": [
            "libssl.so",
            "libssl.so.3",
            "libcrypto.so",
            "libcrypto.so.3",
        ],
        "@platforms//os:macos": [
            "libssl.dylib",
            "libssl.3.dylib",
            "libcrypto.dylib",
            "libcrypto.3.dylib",
        ],
        "//conditions:default": [],
    }),
    # For whatever reason rules_foreign_cc does not respect
    # out_dll_dir parameter and tries to find Windows DLLs in
    # lib instead of bin. Technically, those are binaries,
    # hence, we can treat them as such.
    out_binaries = select({
        "@platforms//os:windows": [
            "libssl-3-x64.dll",
            "libcrypto-3-x64.dll",
        ],
        "//conditions:default": [],
    }),
    deps = [
        "@bzip2//:bz2",
        "@xz//:liblzma",
        "@zlib",
    ],
)
