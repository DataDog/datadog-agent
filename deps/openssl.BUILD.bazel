load("@//bazel/rules:cc_shared_library_wrapper.bzl", "cc_shared_library_wrapper")
load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@//deps/openssl:version.bzl", "OPENSSL_VERSION")
load("@bazel_skylib//rules:select_file.bzl", "select_file")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")

# This should be private. We can fix it in another PR
package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all_srcs",
    srcs = glob(["**"], exclude = ["BUILD.bazel"]),
)

DEFAULT_CONFIG = [
    "no-docs",
    "no-idea",
    "no-mdc2",
    "no-rc5",
    "shared",
    "no-ssl3",
    "no-gost",
    "no-filenames",
    "--libdir=lib",
    # This instructs openssl to dynamically *load* zlib at runtime, not
    # dynamically *link* with it at build time
    "zlib-dynamic",
]

# In order to expose libs to pkg_install
# we need to define filegroups that handle
# output_group attributes generated by configure_make.
# Unfortunately, it creates one output_group per each
# library file. Thus, these constants.
WIN_LIBSSL = "libssl-3-x64.dll"
WIN_LIBCRYPTO = "libcrypto-3-x64.dll"

LINUX_LIBSSL = "libssl.so"
LINUX_LIBCRYPTO = "libcrypto.so"

MACOS_LIBSSL = "libssl.dylib"
MACOS_LIBCRYPTO = "libcrypto.dylib"

# Common script to fix OPENSSLDIR, ENGINESDIR, MODULESDIR and other paths in binaries
# Expects $LIBS to be set by caller to the list of libraries to patch
# In case of OPENSSLDIR there is no additional slash between BUILD_TMPDIR and INSTALL_PREFIX,
# but there is for ENGINESDIR and MODULESDIR.
# TARGET value is a 200 symbols long placeholder that is easy to find and replace after the build.
# It should be long enough to support most paths, but leave room for suffixes like /ssl/private.
FIX_OPENSSL_PATHS = """
    # Fix OpenSSL hardcoded paths in binaries
    SANDBOX="$$BUILD_TMPDIR$$INSTALL_PREFIX"
    TARGET="##########################################################################################PLACEHOLDERDIRECTORY##########################################################################################"

    # OPENSSLDIR: /ssl"
    OLD_PATH="$${SANDBOX}/ssl\\"" NEW_PATH="$${TARGET}/ssl\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL private dir: /ssl/private
    OLD_PATH="$${SANDBOX}/ssl/private" NEW_PATH="$${TARGET}/ssl/private" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL certs dir: /ssl/certs
    OLD_PATH="$${SANDBOX}/ssl/certs" NEW_PATH="$${TARGET}/ssl/certs" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL cert.pem: /ssl/cert.pem
    OLD_PATH="$${SANDBOX}/ssl/cert.pem" NEW_PATH="$${TARGET}/ssl/cert.pem" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    SANDBOX="$$BUILD_TMPDIR/$$INSTALL_PREFIX"
    # ENGINESDIR: /lib/engines-3"
    OLD_PATH="$${SANDBOX}/lib/engines-3\\"" NEW_PATH="$${TARGET}/lib/engines-3\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # MODULESDIR: /lib/ossl-modules"
    OLD_PATH="$${SANDBOX}/lib/ossl-modules\\"" NEW_PATH="$${TARGET}/lib/ossl-modules\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS
"""

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_prefix = select({
        "@platforms//os:windows": "perl",
        "//conditions:default": "",
    }),
    configure_in_place = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        "@platforms//os:windows": "Configure",
    }),
    configure_options = select({
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
        ] + DEFAULT_CONFIG,
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
        ] + DEFAULT_CONFIG,
        # We build openssl for Windows without zlib.
        # Hence, excluding last DEFAULT_CONFIG entry.
        "@platforms//os:windows": [
            "mingw64",
            "no-zlib",
            "no-uplink",
        ] + DEFAULT_CONFIG[:-1] + select({
            "//:fips_mode_enabled": [
                "-DOSSL_WINCTX=datadog-fips-agent",
                "--openssldir='C:/Program Files/Datadog/Datadog Agent/embedded3/ssl'",
            ],
            "//conditions:default": [],
        }),
        "//conditions:default": DEFAULT_CONFIG,
    }),
    install_prefix = select({
        "@platforms//os:windows": "",
        "//conditions:default": "/opt/datadog-agent/embedded",
    }),
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all_srcs",
    out_include_dir = "include",
    out_interface_libs = select({
        "@platforms//os:windows": [
            "libssl.lib",
            "libcrypto.lib",
        ],
        "//conditions:default": [],
    }),
    out_shared_libs = select({
        "@platforms//os:linux": [
            LINUX_LIBSSL,
            LINUX_LIBCRYPTO,
        ],
        "@platforms//os:macos": [
            MACOS_LIBSSL,
            MACOS_LIBCRYPTO,
        ],
        "//conditions:default": [],
    }),
    out_data_dirs = ["lib/pkgconfig"],
    # For whatever reason rules_foreign_cc does not respect
    # out_dll_dir parameter and tries to find Windows DLLs in
    # lib instead of bin. Technically, those are binaries,
    # hence, we can treat them as such.
    out_binaries = select({
        "@platforms//os:windows": [
            WIN_LIBSSL,
            WIN_LIBCRYPTO,
            "openssl.exe",
        ],
        "//conditions:default": ["openssl"],
    }),
    deps = select({
        "//conditions:default": [
            "@zlib//:zlib",
        ],
        "@platforms//os:windows": [],
    }),
    copts = ["-O2"] + select({
        "@platforms//os:windows": ["-DSIO_UDP_NETRESET=_WSAIOW\\(IOC_VENDOR,15\\)"],
        "//conditions:default": [],
    }),
    targets = ["depend", "", "install_sw", "install_ssldirs"],
    # Fix install names, as openssl's build system might not get them right
    postfix_script = select({
        "@platforms//os:macos": """
            # Fix install names
            for lib in libcrypto.dylib libssl.dylib; do
                install_name_tool -id @rpath/$$lib $$INSTALLDIR/lib/$$lib
                for dep in $$(otool -L $$INSTALLDIR/lib/$$lib | grep 'sandbox.*execroot' | awk '{print $$1}'); do
                    install_name_tool -change $$dep "@loader_path/$$(basename $$dep)" $$INSTALLDIR/lib/$$lib
                done
            done
            LIBS="$$INSTALLDIR/lib/libcrypto.dylib $$INSTALLDIR/lib/libssl.dylib"
        """ + FIX_OPENSSL_PATHS + """
        codesign -s - -f $$INSTALLDIR/lib/libcrypto.dylib $$INSTALLDIR/lib/libssl.dylib
        """,
        # Rename .dll.a files into the appropriate extension for import libraries (.lib)
        "@platforms//os:windows": """
        mv $$INSTALLDIR/lib/libcrypto.dll.a $$INSTALLDIR/lib/libcrypto.lib
        mv $$INSTALLDIR/lib/libssl.dll.a $$INSTALLDIR/lib/libssl.lib
        """,
        "//conditions:default": """
            LIBS="$$INSTALLDIR/lib/libcrypto.so $$INSTALLDIR/lib/libssl.so"
        """ + FIX_OPENSSL_PATHS,
    }),
)

cc_shared_library_wrapper(
    name = "openssl_shared",
    input = ":openssl",
)

filegroup(
    name = "headers",
    srcs = [":openssl"],
    output_group = "include",
)

# The dynamic library built with configure/make. Even though the name
# is libssl.so, it is not actually the same folder, so we can have our
# own libssl.so shadow it.
filegroup(
    name = "libssl_shared_file",
    srcs = [":openssl"],
    output_group = select({
        "@platforms//os:windows": WIN_LIBSSL,
        "@platforms//os:macos": MACOS_LIBSSL,
        "@platforms//os:linux": LINUX_LIBSSL,
    }),
)

filegroup(
    name = "libcrypto_shared_file",
    srcs = [":openssl"],
    output_group = select({
        "@platforms//os:windows": WIN_LIBCRYPTO,
        "@platforms//os:macos": MACOS_LIBCRYPTO,
        "@platforms//os:linux": LINUX_LIBCRYPTO,
    }),
)

#####
#
# Packaging
#
#####

so_symlink(
    name = "libssl_with_symlink",
    src = ":libssl_shared_file",
    libname = "libssl",
    version = OPENSSL_VERSION,
)

so_symlink(
    name = "libcrypto_with_symlink",
    src = ":libcrypto_shared_file",
    libname = "libcrypto",
    version = OPENSSL_VERSION,
)

pkg_files(
    name = "openssl_hdr_files",
    srcs = [
        ":headers",
    ],
)

filegroup(
    name = "openssl_exe",
    srcs = [":openssl"],
    output_group = "openssl.exe",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "openssl_exe_file",
    srcs = [":openssl_exe"],
    prefix = "bin",
    target_compatible_with = ["@platforms//os:windows"],
)

filegroup(
    name = "pkgconfig_dir",
    srcs = [":openssl"],
    output_group = "pkgconfig",
)

# "interface libs" are not exposed as output groups, therefore we use a more versatile rule to get these
select_file(
    name = "libcrypto_import_lib",
    srcs = ":openssl",
    subpath = "openssl/lib/libcrypto.lib",
    target_compatible_with = ["@platforms//os:windows"],
)

select_file(
    name = "libssl_import_lib",
    srcs = ":openssl",
    subpath = "openssl/lib/libssl.lib",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "import_lib_files",
    srcs = [
        ":libcrypto_import_lib",
        ":libssl_import_lib",
    ],
    prefix = "lib",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "openssl_pkgconfig",
    srcs = [":pkgconfig_dir"],
    prefix = "lib",
)

filegroup(
    name = "openssl_bin",
    srcs = [":openssl"],
    output_group = "openssl",
)

pkg_files(
    name = "openssl_bin_file",
    srcs = [":openssl_bin"],
    prefix = "bin",
    attributes = pkg_attributes(
        mode = "0755",
    ),
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

pkg_install(
    name = "install",
    srcs = [
        ":openssl_hdr_files",
        ":libssl_with_symlink",
        ":libcrypto_with_symlink",
    ] + select({
        "@platforms//os:windows": [
            ":import_lib_files",
            ":openssl_exe_file",
        ],
        "//conditions:default": [
            ":openssl_bin_file",
            ":openssl_pkgconfig",
        ],
    }),
)
