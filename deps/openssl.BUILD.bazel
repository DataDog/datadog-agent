load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all",
    srcs = glob(["**"]),
)

DEFAULT_CONFIGURE_OPTIONS = [
    "--libdir=lib",
    "no-idea",
    "no-mdc2",
    "no-rc5",
    "shared",
    "no-ssl3",
    "no-gost",
]

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        # Windows build isn't guaranteed to work.
        # This is a preliminary structure.
        "@platforms//os:windows": "perl.exe ./Configure",
    }),
    configure_options = select({
        "@platforms//os:windows": [
            "no-zlib",
            "no-uplink",
        ] + DEFAULT_CONFIGURE_OPTIONS,
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
            "zlib",
        ] + DEFAULT_CONFIGURE_OPTIONS,
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
            "zlib",
        ] + DEFAULT_CONFIGURE_OPTIONS,
        "//conditions:default": [
            "zlib",
        ] + DEFAULT_CONFIGURE_OPTIONS,
    }),
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all",
    out_shared_libs = select({
        "@platforms//os:linux": [
            "libssl.so",
            "libssl.so.3",
            "libcrypto.so",
            "libcrypto.so.3",
        ],
        "@platforms//os:macos": [
            "libssl.dylib",
            "libssl.3.dylib",
            "libcrypto.dylib",
            "libcrypto.3.dylib",
        ],
        "@platforms//os:windows": [
            "libssl.dll",
            "libssl-3.dll",
            "libcrypto.dll",
            "libcrypto-3.dll",
        ],
        "//conditions:default": [],
    }),
    deps = [
        "@bzip2//:bz2",
        "@xz//:lzma",
        "@zlib",
    ],
)
