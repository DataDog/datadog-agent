load("@bazel_skylib//lib:selects.bzl", "selects")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all",
    srcs = glob(["**"], exclude = ["BUILD.bazel"]),
)

DEFAULT_CONFIG = [
    "no-docs",
    "no-idea",
    "no-mdc2",
    "no-rc5",
    "shared",
    "no-ssl3",
    "no-gost",
    "--libdir=lib",
    "zlib-dynamic",
]

# In order to expose libs to pkg_install
# we need to define filegroups that handle
# output_group attributes generated by configure_make.
# Unfortunately, it creates one output_group per each
# library file. Thus, these constants.
WIN_LIBS = [
    "libssl-3-x64.dll",
    "libcrypto-3-x64.dll",
]

LINUX_LIBS = [
    "libssl.so",
    "libssl.so.3",
    "libcrypto.so",
    "libcrypto.so.3",
]

MACOS_LIBS = [
    "libssl.dylib",
    "libssl.3.dylib",
    "libcrypto.dylib",
    "libcrypto.3.dylib",
]

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_prefix = select({
        "@platforms//os:windows": "perl",
        "//conditions:default": "",
    }),
    configure_in_place = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        "@platforms//os:windows": "Configure",
    }),
    configure_options = select({
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
        ] + DEFAULT_CONFIG,
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
        ] + DEFAULT_CONFIG,
        # We build openssl for Windows without zlib.
        # Hence, excluding last DEFAULT_CONFIG entry.
        "@platforms//os:windows": [
            "mingw64",
            "no-zlib",
            "no-uplink",
        ] + DEFAULT_CONFIG[:-1],
        "//conditions:default": DEFAULT_CONFIG,
    }),
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all",
    out_include_dir = "include",
    out_static_libs = select({
        "@platforms//os:windows": [
            "libssl.dll.a",
            "libcrypto.dll.a",
        ],
        "//conditions:default": [],
    }),
    out_shared_libs = select({
        "@platforms//os:linux": LINUX_LIBS,
        "@platforms//os:macos": MACOS_LIBS,
        "//conditions:default": [],
    }),
    out_pc_dir = "lib/pkgconfig",
    # For whatever reason rules_foreign_cc does not respect
    # out_dll_dir parameter and tries to find Windows DLLs in
    # lib instead of bin. Technically, those are binaries,
    # hence, we can treat them as such.
    out_binaries = select({
        "@platforms//os:windows": WIN_LIBS + ["openssl.exe"],
        "//conditions:default": [],
    }),
    deps = select({
        "//conditions:default": [
            "@bzip2//:libbz2",
            "@xz//:liblzma",
            "@zlib//:zlib",
        ],
        "@platforms//os:windows": [],
    }),
    copts = ["-O2"],
    targets = ["depend", "", "install_sw", "install_ssldirs"],
    # Fix install names, as openssl's build system might not get them right
    postfix_script = select({
        "@platforms//os:macos": "".join([
                """
                install_name_tool -id @rpath/{lib} \
                        $$INSTALLDIR/lib/{lib}
                for dep in $$(otool -L $$INSTALLDIR/lib/{lib} | grep 'sandbox.*execroot' | awk '{{print $$1}}'); do
                    install_name_tool -change $$dep "@loader_path/$$(basename $$dep)" \
                            $$INSTALLDIR/lib/{lib};
                done
                """.format(lib = lib)
            for lib in ["libcrypto.3.dylib", "libcrypto.dylib", "libssl.3.dylib", "libssl.dylib"]
        ]),
        "//conditions:default": ""
    }),
)

[
    filegroup(
        name = "lib_linux_" + libname,
        srcs = [":openssl"],
        output_group = libname
    )
    for libname in LINUX_LIBS
]

[
    filegroup(
        name = "lib_macos_" + libname,
        srcs = [":openssl"],
        output_group = libname
    )
    for libname in MACOS_LIBS
]

[
    filegroup(
        name = "lib_win_" + libname,
        srcs = [":openssl"],
        output_group = libname
    )
    for libname in WIN_LIBS
]

filegroup(
    name = "headers",
    srcs = [":openssl"],
    output_group = "include",
)

pkg_files(
    name = "openssl_hdr_files",
    srcs = [
        ":headers",
    ],
)

filegroup(
    name = "openssl_exe",
    srcs = [":openssl"],
    output_group = "openssl.exe",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "openssl_exe_file",
    srcs = [":openssl_exe"],
    prefix = "bin",
    target_compatible_with = ["@platforms//os:windows"],
)

filegroup(
    name = "pkgconfig_dir",
    srcs = [":openssl"],
    output_group = "pkgconfig",
)

filegroup(
    name = "libcrypto_dll_a",
    srcs = [":openssl"],
    output_group = "libcrypto.dll.a",
    target_compatible_with = ["@platforms//os:windows"],
)

filegroup(
    name = "libssl_dll_a",
    srcs = [":openssl"],
    output_group = "libssl.dll.a",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "dll_a",
    srcs = [
        ":libcrypto_dll_a",
        ":libssl_dll_a",
    ],
    prefix = "lib",
    target_compatible_with = ["@platforms//os:windows"],
)

pkg_files(
    name = "openssl_pkgconfig",
    srcs = [":pkgconfig_dir"],
    prefix = "lib",
)

pkg_files(
    name = "openssl_libs",
    srcs = select({
        "@platforms//os:windows": [":lib_win_" + libname for libname in WIN_LIBS],
        "@platforms//os:macos": [":lib_macos_" + libname for libname in MACOS_LIBS],
        "@platforms//os:linux": [":lib_linux_" + libname for libname in LINUX_LIBS],
    }),
    prefix = "lib",
)

pkg_install(
    name = "install",
    srcs = [
        ":openssl_hdr_files",
        ":openssl_libs",
    ] + select({
        "@platforms//os:windows": [
            ":dll_a",
            ":openssl_exe_file",
        ],
        "//conditions:default": [
            ":openssl_pkgconfig",
        ],
    }),
)
