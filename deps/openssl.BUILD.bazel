load("@//bazel/rules:foreign_cc_shared_wrapper.bzl", "foreign_cc_shared_wrapper")
load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@//deps/openssl:version.bzl", "OPENSSL_VERSION")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files")

# This should be private. We can fix it in another PR
package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all_srcs",
    srcs = glob(["**"], exclude = ["BUILD.bazel"]),
)

config_setting(
    name = "fips_windows",
    constraint_values = [
        "@platforms//os:windows",
    ],
    flag_values = {"@@//packages/agent:flavor": "fips"},
)

DEFAULT_CONFIG = [
    "no-docs",
    "no-idea",
    "no-mdc2",
    "no-rc5",
    "shared",
    "no-ssl3",
    "no-gost",
    "no-filenames",
    "--libdir=lib",
    # This instructs openssl to dynamically *load* zlib at runtime, not
    # dynamically *link* with it at build time
    "zlib-dynamic",
]

# In order to expose libs to pkg_install
# we need to define filegroups that handle
# output_group attributes generated by configure_make.
# Unfortunately, it creates one output_group per each
# library file. Thus, these constants.
WIN_LIBSSL = "libssl-3-x64.dll"
WIN_LIBCRYPTO = "libcrypto-3-x64.dll"

LINUX_LIBSSL = "libssl.so"
LINUX_LIBCRYPTO = "libcrypto.so"

MACOS_LIBSSL = "libssl.dylib"
MACOS_LIBCRYPTO = "libcrypto.dylib"

# Common script to fix OPENSSLDIR, ENGINESDIR, MODULESDIR and other paths in binaries
# Expects $LIBS to be set by caller to the list of libraries to patch
# OpenSSL stores paths in two forms:
#   1. Display strings for `openssl version -a` ending with " (e.g., MODULESDIR: "/path")
#   2. Internal runtime paths without quotes used for actually loading modules
# Both forms must be replaced for FIPS to work correctly.
# In case of OPENSSLDIR there is no additional slash between BUILD_TMPDIR and INSTALL_PREFIX,
# but there is for ENGINESDIR and MODULESDIR.
# TARGET value is a 200 symbols long placeholder that is easy to find and replace after the build.
# It should be long enough to support most paths, but leave room for suffixes like /ssl/private.
FIX_OPENSSL_PATHS = """
    # Fix OpenSSL hardcoded paths in binaries
    TARGET="##########################################################################################PLACEHOLDERDIRECTORY##########################################################################################"
    SANDBOX="$$BUILD_TMPDIR/$$INSTALL_PREFIX"

    # === OPENSSLDIR paths ===
    # OPENSSLDIR display string: /ssl"
    if [ $$(echo $${SANDBOX} | wc -c) -lt $$(echo $${TARGET} | wc -c) ]; then
        echo "The new path placeholder is larger than the target it should replace replace ($${SANDBOX})";
        exit 1;
    fi
    OLD_PATH="$${SANDBOX}/ssl\\"" NEW_PATH="$${TARGET}/ssl\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # OPENSSLDIR internal path (bare, no quote) - used for actual config lookup
    OLD_PATH="$${SANDBOX}/ssl" NEW_PATH="$${TARGET}/ssl" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL private dir: /ssl/private
    OLD_PATH="$${SANDBOX}/ssl/private" NEW_PATH="$${TARGET}/ssl/private" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL certs dir: /ssl/certs
    OLD_PATH="$${SANDBOX}/ssl/certs" NEW_PATH="$${TARGET}/ssl/certs" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # SSL cert.pem: /ssl/cert.pem
    OLD_PATH="$${SANDBOX}/ssl/cert.pem" NEW_PATH="$${TARGET}/ssl/cert.pem" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # === ENGINESDIR and MODULESDIR paths ===
    # ENGINESDIR display string: /lib/engines-3"
    OLD_PATH="$${SANDBOX}/lib/engines-3\\"" NEW_PATH="$${TARGET}/lib/engines-3\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # ENGINESDIR internal path (bare, no quote) - used for actual engine loading
    OLD_PATH="$${SANDBOX}/lib/engines-3" NEW_PATH="$${TARGET}/lib/engines-3" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # MODULESDIR display string: /lib/ossl-modules"
    OLD_PATH="$${SANDBOX}/lib/ossl-modules\\"" NEW_PATH="$${TARGET}/lib/ossl-modules\\"" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS

    # MODULESDIR internal path (bare, no quote) - used for actual provider loading (CRITICAL for FIPS!)
    OLD_PATH="$${SANDBOX}/lib/ossl-modules" NEW_PATH="$${TARGET}/lib/ossl-modules" perl -0777 -pi -e 's/\\Q$$ENV{OLD_PATH}\\E/$$ENV{NEW_PATH} . ("\\x00" x (length($$ENV{OLD_PATH}) - length($$ENV{NEW_PATH})))/ge' $$LIBS
"""

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_prefix = select({
        "@platforms//os:windows": "perl",
        "//conditions:default": "",
    }),
    configure_in_place = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        "@platforms//os:windows": "Configure",
    }),
    configure_options = select({
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
        ] + DEFAULT_CONFIG,
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
        ] + DEFAULT_CONFIG,
        # We build openssl for Windows without zlib.
        # Hence, excluding last DEFAULT_CONFIG entry.
        "@platforms//os:windows": [
            "mingw64",
            "no-zlib",
            "no-uplink",
        ] + DEFAULT_CONFIG[:-1],
        "//conditions:default": DEFAULT_CONFIG,
    }) + select({
        ":fips_windows": [
            "-DOSSL_WINCTX=datadog-fips-agent",
            "--openssldir='C:/Program Files/Datadog/Datadog Agent/embedded3/ssl'",
        ],
        "//conditions:default": [],
    }),
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    install_prefix = select({
        "@platforms//os:windows": "",
        # Provide a long value to ensure we end up with a path to openssl in the sandbox that
        # is longer than the placeholder we will replace it with as part of the postfix script
        "//conditions:default": "placeholder" + "_" * 50,
    }),
    lib_source = ":all_srcs",
    out_include_dir = "include",
    out_interface_libs = select({
        "@platforms//os:windows": [
            "libssl.lib",
            "libcrypto.lib",
        ],
        "//conditions:default": [],
    }),
    out_shared_libs = select({
        "@platforms//os:linux": [
            LINUX_LIBSSL,
            LINUX_LIBCRYPTO,
        ],
        "@platforms//os:macos": [
            MACOS_LIBSSL,
            MACOS_LIBCRYPTO,
        ],
        "//conditions:default": [],
    }),
    out_data_dirs = select({
        "@//packages/agent:fips_flavor": [
            "ssl",
            "lib/ossl-modules",
            "lib/engines-3",
        ],
        "//conditions:default": [],
    }),
    # For whatever reason rules_foreign_cc does not respect
    # out_dll_dir parameter and tries to find Windows DLLs in
    # lib instead of bin. Technically, those are binaries,
    # hence, we can treat them as such.
    out_binaries = select({
        "@platforms//os:windows": [
            WIN_LIBSSL,
            WIN_LIBCRYPTO,
            "openssl.exe",
        ],
        "//conditions:default": ["openssl"],
    }),
    deps = select({
        "//conditions:default": [
            "@zlib//:zlib",
        ],
        "@platforms//os:windows": [],
    }),
    copts = ["-O2"] + select({
        "@platforms//os:windows": ["-DSIO_UDP_NETRESET=_WSAIOW\\(IOC_VENDOR,15\\)"],
        "//conditions:default": [],
    }),
    targets = ["depend", "", "install_sw", "install_ssldirs"],
    # Fix install names, as openssl's build system might not get them right
    # Maybe for macos, @loader_path should be @rpath. That helped get curl_bin
    # to link
    postfix_script = select({
        "@platforms//os:macos": """
            # Fix install names
            for lib in libcrypto.dylib libssl.dylib; do
                install_name_tool -id @rpath/$$lib $$INSTALLDIR/lib/$$lib
                for dep in $$(otool -L $$INSTALLDIR/lib/$$lib | grep 'sandbox.*execroot' | awk '{print $$1}'); do
                    install_name_tool -change $$dep "@loader_path/$$(basename $$dep)" $$INSTALLDIR/lib/$$lib
                done
            done
            LIBS="$$INSTALLDIR/lib/libcrypto.dylib $$INSTALLDIR/lib/libssl.dylib"
        """ + FIX_OPENSSL_PATHS + """
        codesign -s - -f $$INSTALLDIR/lib/libcrypto.dylib $$INSTALLDIR/lib/libssl.dylib
        """,
        # Rename .dll.a files into the appropriate extension for import libraries (.lib)
        "@platforms//os:windows": """
        mv $$INSTALLDIR/lib/libcrypto.dll.a $$INSTALLDIR/lib/libcrypto.lib
        mv $$INSTALLDIR/lib/libssl.dll.a $$INSTALLDIR/lib/libssl.lib
        """,
        "//conditions:default": """
            LIBS="$$INSTALLDIR/lib/libcrypto.so $$INSTALLDIR/lib/libssl.so"
        """ + FIX_OPENSSL_PATHS,
    }),
)

foreign_cc_shared_wrapper(
    name = "openssl_shared",
    input = ":openssl",
)

# The dynamic library built with configure/make. Even though the name
# is libssl.so, it is not actually the same folder, so we can have our
# own libssl.so shadow it.
filegroup(
    name = "libssl_shared_file",
    srcs = [":openssl"],
    output_group = select({
        "@platforms//os:windows": WIN_LIBSSL,
        "@platforms//os:macos": MACOS_LIBSSL,
        "@platforms//os:linux": LINUX_LIBSSL,
    }),
)

filegroup(
    name = "libcrypto_shared_file",
    srcs = [":openssl"],
    output_group = select({
        "@platforms//os:windows": WIN_LIBCRYPTO,
        "@platforms//os:macos": MACOS_LIBCRYPTO,
        "@platforms//os:linux": LINUX_LIBCRYPTO,
    }),
)

so_symlink(
    name = "libssl_with_symlink",
    src = ":libssl_shared_file",
    libname = "libssl",
    version = OPENSSL_VERSION,
)

so_symlink(
    name = "libcrypto_with_symlink",
    src = ":libcrypto_shared_file",
    libname = "libcrypto",
    version = OPENSSL_VERSION,
)

filegroup(
    name = "openssl_binary",
    srcs = [":openssl"],
    output_group = select({
        "@platforms//os:windows": "openssl.exe",
        "//conditions:default": "openssl",
    }),
)

pkg_files(
    name = "openssl_files",
    srcs = [":openssl"],
    strip_prefix = "openssl",
    excludes = [
        # Binaries require custom attributes
        ":openssl_binary",
        # Dynamic require separate symlink post-processing
        ":libssl_shared_file",
        ":libcrypto_shared_file",
        # We don't need static libraries in the package
        ":libssl_static",
        ":libcrypto_static",
    ],
)

pkg_files(
    name = "openssl_bin_files",
    srcs = [":openssl_binary"],
    strip_prefix = "openssl",
    attributes = pkg_attributes(
        mode = "0755",
    ),
)

pkg_filegroup(
    name = "all_files",
    srcs = [
        ":openssl_bin_files",
    ] + select({
        "@//packages/agent:fips_flavor": [
            ":openssl_files",
            ":libssl_with_symlink",
            ":libcrypto_with_symlink",
        ],
        "@platforms//os:windows": [],
        "//conditions:default": [
            ":openssl_files",
            ":libssl_with_symlink",
            ":libcrypto_with_symlink",
        ],
    }),
    prefix = select({
        "@platforms//os:windows": "embedded3",
        "//conditions:default": "embedded",
    }),
)

pkg_install(
    name = "install",
    srcs = [
        ":all_files",
    ],
)
