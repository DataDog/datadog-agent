load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "all",
    srcs = glob(["**"]),
)

DEFAULT_CONFIG = [
    "no-docs",
    "no-idea",
    "no-mdc2",
    "no-rc5",
    "shared",
    "no-ssl3",
    "no-gost",
    "zlib",
]

configure_make(
    name = "openssl",
    # TODO(team:agent-build): Find a better way to utilize more than 1 core
    args = [
        "-j 16",
    ],
    configure_prefix = select({
        "@platforms//os:windows": "perl",
        "//conditions:default": "",
    }),
    configure_in_place = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    configure_command = select({
        "@platforms//os:linux": "config",
        "@platforms//os:macos": "Configure",
        "@platforms//os:windows": "Configure",
    }),
    configure_options = select({
        "@//:macos_arm64": [
            "darwin64-arm64-cc",
        ] + DEFAULT_CONFIG,
        "@//:macos_x86_64": [
            "darwin64-x86_64-cc",
        ] + DEFAULT_CONFIG,
        # We build openssl for Windows without zlib.
        # Hence, excluding last DEFAULT_CONFIG entry.
        "@platforms//os:windows": [
            "mingw64",
            "no-zlib",
            "no-uplink",
        ] + DEFAULT_CONFIG[:-1],
        "//conditions:default": [],
    }),
    env = select({
        "@platforms//os:macos": {
            # For some reason AR is set to libtool on macos
            # and that breaks the build.
            "AR": "ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all",
    out_shared_libs = select({
        "@platforms//os:linux": [
            "libssl.so",
            "libssl.so.3",
            "libcrypto.so",
            "libcrypto.so.3",
        ],
        "@platforms//os:macos": [
            "libssl.dylib",
            "libssl.3.dylib",
            "libcrypto.dylib",
            "libcrypto.3.dylib",
        ],
        "//conditions:default": [],
    }),
    # For whatever reason rules_foreign_cc does not respect
    # out_dll_dir parameter and tries to find Windows DLLs in
    # lib instead of bin. Technically, those are binaries,
    # hence, we can treat them as such.
    out_binaries = select({
        "@platforms//os:windows": [
            "libssl-3-x64.dll",
            "libcrypto-3-x64.dll",
        ],
        "//conditions:default": [],
    }),
    deps = select({
        "//conditions:default": [
            "@bzip2//:bz2",
            "@xz//:liblzma",
            "@zlib",
        ],
        "@platforms//os:windows": [],
    }),
    targets = ["depend", "", "install_sw", "install_ssldirs"],
)

pkg_files(
    name = "openssl_lib_files",
    srcs = [":openssl"],
    prefix = "lib",
)

pkg_install(
    name = "install",
    srcs = [
        ":openssl_lib_files",
    ],
)
