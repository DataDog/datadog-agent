load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_cc//cc:defs.bzl", "cc_library")

COMMON_COPTS = ["-O3"]

copy_file(
    name = "libltdl_config_h",
    src = select({
        "@platforms//cpu:x86_64": "x86_64/libltdl/config.h",
        "//conditions:default": "@platforms//:incompatible",
    }),
    out = "libltdl/config.h",
)

copy_file(
    name = "config_h",
    src = select({
        "@platforms//cpu:x86_64": "x86_64/config.h",
        "//conditions:default": "@platforms//:incompatible",
    }),
    out = "config.h",
)

# Built-in libltdl library to address "--with-included-ltdl" configure option.
cc_library(
    name = "libltdl",
    # This header has to be exposed along with the library to address
    # "--enable-ltdl-install" configure option.
    hdrs = ["libltdl/ltdl.h"],
    srcs = glob(
        ["libltdl/**/*.c", "libltdl/**/*.h"],
        exclude = [
            # Non-Unix loaders
            "libltdl/loaders/load_add_on.c",
            "libltdl/loaders/dyld.c",
            "libltdl/loaders/loadlibrary.c",
            "libltdl/loaders/shl_load.c",
            # Non-glibc implementation that is not needed
            "libltdl/lt__argz.c",
        ],
    ) + [":libltdl_config_h"],
    visibility = ["//visibility:public"],
    copts = COMMON_COPTS + ["-DLTDL"],
    # "." is needed to make sure config.h is found.
    includes = ["libltdl/libltdl", "libltdl/."],
)

cc_library(
    name = "log",
    # :config_h cannot be inside of the glob() because
    # actions are executed during execution phase, not during analysis phase
    # and glob is evaluated during analysis phase.
    srcs = glob(["log/**/*.c", "include/*.h"]) + [":config_h"],
    visibility = ["//visibility:public"],
    copts = COMMON_COPTS,
    includes = [".", "include"],
)

cc_library(
    name = "extras",
    srcs = glob(["extras/**/*.c", "include/*.h"], exclude = ["extras/vms.c"]) + [":config_h"],
    visibility = ["//visibility:public"],
    copts = COMMON_COPTS,
    includes = [".", "include"],
)

cc_library(
    name = "ini",
    srcs = glob(["ini/**/*.c", "include/*.h"]) + [":config_h"],
    visibility = ["//visibility:public"],
    copts = COMMON_COPTS,
    includes = [".", "include"],
)
