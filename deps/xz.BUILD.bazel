"""Agent specific BUILD file for xz-utils / liblzma."""

load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files", "pkg_mklink")
load("@@//bazel/rules:pkgconfig.bzl", "gen_pkgconfig")

package(
    default_applicable_licenses = [":license"],
    default_visibility = ["//visibility:private"],
)

alias(
    name = "xz",
    actual = "@xz//:lzma",
)

# Warning: starting from 5.5.2, the license switches to BSD-0
license(
    name = "license",
    license_kinds = ["@rules_license//licenses/generic:unencumbered"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

exports_files([
    "LICENSE",
])

copy_file(
    name = "copy_config",
    src = selects.with_or({
        "@//:linux_arm64": "generated/config.lzma-linux-arm64.h",
        "@//:linux_x86_64": "generated/config.lzma-linux-x86_64.h",
        "@//:macos_arm64": "generated/config.lzma-osx-arm64.h",
        "@//:macos_x86_64": "generated/config.lzma-osx-x86_64.h",
        "@platforms//os:windows": "generated/config.lzma-windows.h",
    }),
    out = "src/liblzma/api/config.h",  # minimize the number of exported include paths
)

_MAIN_HEADER = glob([
    "src/liblzma/api/lzma.h",
])

_API_HEADERS = glob([
    "src/liblzma/api/lzma/*.h",
])

cc_library(
    name = "lzma",
    srcs = [
        "src/common/tuklib_cpucores.c",
        "src/common/tuklib_physmem.c",
    ] + glob(
        [
            "src/**/*.h",
            "src/liblzma/**/*.c",
        ],
        exclude = [
            "src/liblzma/check/crc*_small.c",
            "src/liblzma/**/*_tablegen.c",
        ],
    ),
    hdrs = _MAIN_HEADER + _API_HEADERS,
    copts = select({
        "@platforms//os:windows": [],
        "//conditions:default": ["-std=c99"],
    }),
    defines = select({
        "@platforms//os:windows": ["LZMA_API_STATIC"],
        "//conditions:default": [],
    }),
    local_defines = [
        "TUKLIB_SYMBOL_PREFIX=lzma_",
    ],
    linkopts = select({
        "@platforms//os:android": [],
        "//conditions:default": ["-lpthread"],
    }),
    linkstatic = select({
        "@platforms//os:windows": True,
        "//conditions:default": False,
    }),
    strip_include_prefix = "src/liblzma/api",  # Allows public header without the path and without COPTS -I or includes = []
    visibility = ["//visibility:public"],
    deps = [
        "//:lzma_src_common",
        "//:lzma_src_liblzma",
        "//:lzma_src_liblzma_api",
        "//:lzma_src_liblzma_check",
        "//:lzma_src_liblzma_common",
        "//:lzma_src_liblzma_delta",
        "//:lzma_src_liblzma_lz",
        "//:lzma_src_liblzma_lzma",
        "//:lzma_src_liblzma_rangecoder",
        "//:lzma_src_liblzma_simpler",
    ],
)

# cc_library(
#     name = "xz_win_resources",
#     srcs = ["xz_w32res.rc"],
# )

cc_library(
    name = "lzma_src_common",
    srcs = [
        "src/common/tuklib_exit.c",
        "src/common/tuklib_progname.c",
    ],
    hdrs = glob(["src/common/*.h"]),
    defines = ["HAVE_CONFIG_H"] + select({
        "@platforms//os:windows": [
            "LZMA_API_STATIC",
            "TUKLIB_GETTEXT=0",  # Disable libintl for windows
        ],
        "//conditions:default": [],
    }),
    strip_include_prefix = "src/common",
    deps = [
        "//:lzma_src_liblzma_api",
    ],
)

cc_library(
    name = "lzma_src_liblzma",
    strip_include_prefix = "src/liblzma",
)

cc_library(
    name = "lzma_src_liblzma_api",
    hdrs = [
        "src/liblzma/api/config.h",  # Generated above, so missed by glob. In srcs so it's not public like the other headers
    ] + glob(
        [
            "src/liblzma/api/**/*.h",
        ],
        exclude = [
            "src/liblzma/api/lzma.h",  # The public header, only used in hdrs of main lib (//visibility:public)
        ],
    ),
    strip_include_prefix = "src/liblzma/api",
)

cc_library(
    name = "lzma_src_liblzma_check",
    hdrs = glob(["src/liblzma/check/*.h"]),
    strip_include_prefix = "src/liblzma/check",
)

cc_library(
    name = "lzma_src_liblzma_common",
    hdrs = glob(["src/liblzma/common/*.h"]),
    includes = ["src/liblzma"],  # Needed as well as some usages use common/*.h instead of just the header
    strip_include_prefix = "src/liblzma/common",
)

cc_library(
    name = "lzma_src_liblzma_delta",
    hdrs = glob(["src/liblzma/delta/*.h"]),
    strip_include_prefix = "src/liblzma/delta",
)

cc_library(
    name = "lzma_src_liblzma_lz",
    hdrs = glob(["src/liblzma/lz/*.h"]),
    strip_include_prefix = "src/liblzma/lz",
)

cc_library(
    name = "lzma_src_liblzma_lzma",
    hdrs = glob(["src/liblzma/lzma/*.h"]),
    strip_include_prefix = "src/liblzma/lzma",
)

cc_library(
    name = "lzma_src_liblzma_rangecoder",
    hdrs = glob(["src/liblzma/rangecoder/*.h"]),
    strip_include_prefix = "src/liblzma/rangecoder",
)

cc_library(
    name = "lzma_src_liblzma_simpler",
    hdrs = glob(["src/liblzma/simple/*.h"]),
    strip_include_prefix = "src/liblzma/simple",
)

# cc_shared_library(
#     name = "liblzma_so",
#     shared_lib_name = "liblzma.so.5.4.2",
#     deps = [":lzma"],
#     user_link_flags = select({
#         "@platforms//os:windows": [],
#         "@platforms//os:macos": ["-Wl,-install_name","-Wl,'@rpath/liblzma.so.5'",],
#         "//conditions:default": [],
#     }),
#     visibility = ["//visibility:public"],
# )

# pkg_mklink(
#     name = "so_major_minor",
#     link_name = "liblzma.so.5.4.2",
#     target = ":liblzma_so",
# )
#
# pkg_mklink(
#     name = "so_major",
#     link_name = "liblzma.so.5",
#     target = ":liblzma_so",
# )
#
# pkg_mklink(
#     name = "so",
#     link_name = "liblzma.so",
#     target = ":liblzma_so",
# )

pkg_files(
    name = "lib_files",
    srcs = [
        ":lzma",
        # TODO(agent-build): These are silently ingored by rules_pkg.  We have to
        # fix that upstream, or find another method.
        # ":so_major_minor",
        # ":so_major",
        # ":so",
    ],
    prefix = "lib",
)

pkg_files(
    name = "main_header",
    srcs = _MAIN_HEADER,
    prefix = "include",
)

pkg_files(
    name = "api_headers",
    srcs = _API_HEADERS,
    prefix = "include/lzma",
)

gen_pkgconfig(
    name = "gen_pkgconfig",
    template = "@@//deps/pkgconfig:liblzma.pc.in",
    version = "5.4.2",
)

pkg_files(
    name = "pkgconfig",
    srcs = [":gen_pkgconfig"],
    prefix = "lib/pkgconfig",
)

pkg_install(
    name = "install",
    srcs = [
        ":main_header",
        ":api_headers",
        ":lib_files",
        ":pkgconfig",
    ],
)
