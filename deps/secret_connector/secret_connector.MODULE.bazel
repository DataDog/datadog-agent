# Secret connector

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

VERSION = "1.5.0"

# Define URLs for each platform
secret_generic_urls = {
    "linux": {
        "x86_64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-linux-amd64.tar.gz",
        "arm64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-linux-arm64.tar.gz",
    },
    "windows": {
        "x86_64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-windows-amd64.zip",
        "arm64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-windows-arm64.zip",
    },
    "macos": {
        "x86_64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-darwin-amd64.tar.gz",
        "arm64": "https://github.com/DataDog/datadog-secret-backend/releases/download/v{version}/datadog-secret-backend-darwin-arm64.tar.gz",
    },
}

# Define SHA256 checksums for verification
# Note: These should be updated for each version
secret_generic_sha256 = {
    "linux": {
        "x86_64": "932d78686207f4d428e266c7afcea0fe4cba26fbad9f3b5e6686ef510bcc985c",
        "arm64": "f20c043af96d8982283ee065e3fe5eca527656916d5e2e79e308a51e959fba10",
    },
    "windows": {
        "x86_64": "2b1fc0523cb9e5ff48f70a779b4cdc3d0b5d58ef929e41a0935e35dd01c6b636",
        "arm64": "5d28d6db8663e3e428a702608caf4e5ac4008ed901f46d0feb2965e7f7603146",
    },
    "macos": {
        "x86_64": "583fac3054ba1630fc115bda45dd3aceb93f3c4bcebf366dcad17f39320b2d91",
        "arm64": "1bc6998789553aa26d2b248d1b2aac5b6a1807d51865eca495065fb44be047f6",
    },
}

[
    # Define URLs for each architecture. This is used for the Linux build only; Windows and Darwin are not supported yet.
    # When we do those, we can make this parameterized.
    http_archive(
        name = "secret_connector_{platform}_{arch}".format(
            arch = arch,
            platform = platform,
        ),
        files = {
            "BUILD.bazel": "//deps/secret_connector:overlay.BUILD.bazel",
        },
        sha256 = secret_generic_sha256[platform][arch],
        urls = [
            secret_generic_urls[platform][arch].format(version = VERSION),
        ],
    )
    for platform in ("linux", "macos", "windows")
    for arch in ("x86_64", "arm64")
]
