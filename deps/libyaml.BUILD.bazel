"""BUILD file for libyaml."""

load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")

VERSION = "0.2.5"

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:MIT"],
    license_text = "LICENSE",
    copyright_notice = """
    Copyright (c) 2017-2020 Ingy d√∂t Net
    Copyright (c) 2006-2016 Kirill Simonov
    """,
    visibility = ["//visibility:public"],
)

cc_library(
    name = "libyaml",
    srcs = [
        "src/api.c",
        "src/dumper.c",
        "src/emitter.c",
        "src/loader.c",
        "src/parser.c",
        "src/reader.c",
        "src/scanner.c",
        "src/writer.c",
        "src/yaml_private.h",
    ],
    hdrs = ["include/yaml.h"],
    copts = ["-w"],
    includes = ["include"],
    local_defines = [
        "YAML_VERSION_MAJOR=0",
        "YAML_VERSION_MINOR=2",
        "YAML_VERSION_PATCH=5",
        'YAML_VERSION_STRING=\\"0.2.5\\"',
    ],
    visibility = ["//visibility:public"],
)

cc_shared_library(
    name = "libyaml_so",
    deps = [":libyaml"],
    visibility = ["//visibility:public"],
)

so_symlink(
    name = "lib_files",
    src = ":libyaml_so",
    libname = "libyaml",
    version = VERSION,
)

pkg_files(
    name = "headers",
    srcs = ["include/yaml.h"],
    prefix = "include",
)

expand_template(
    name = "gen_pkgconfig",
    out = "yaml-0.1.pc",
    substitutions = {
        "@PACKAGE_VERSION@": VERSION,
        "@prefix@": "##PREFIX##",
        "@exec_prefix@": "${prefix}",
        "@includedir@": "${prefix}/include",
        "@libdir@": "${prefix}/lib",
    },
    template = "yaml-0.1.pc.in",
)

pkg_files(
    name = "pkgconfig",
    srcs = [":gen_pkgconfig"],
    prefix = "lib/pkgconfig",
)

pkg_install(
    name = "install",
    srcs = [
        ":lib_files",
        ":headers",
        ":pkgconfig",
    ],
)
