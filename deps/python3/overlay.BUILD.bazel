load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_shared_library.bzl", "cc_shared_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files", "strip_prefix", "pkg_attributes")
load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

load("@@//deps:python3/modules.bzl", "PYTHON_MODULES", "PYTHON_CORE_MODULES_SRCS", "PYTHON_FROZEN_MODULES")
load("@@//deps:python3/freeze.bzl", "python_freeze")

_VERSION = "3.13.7"
_VERSION_FOR_SO = ".".join(_VERSION.split('.')[:2])

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:Python-2.0"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "copy_pyconfig",
    src = select({
        "@//:linux_arm64": "config-linux-arm64.h",
        "@//:linux_x86_64": "config-linux-x86_64.h",
        "@//:macos_arm64": "config-macos-arm64.h",
        "@//:macos_x86_64": "config-macos-x86_64.h",
    }),
    out = "pyconfig.h",  # minimize the number of exported include paths
)

COMMON_DEFINES = [
    "NDEBUG",
]

PY_CORE_DEFINES = [
    "Py_BUILD_CORE",
]

PY_BUILTIN_MODULE_DEFINES = [
    "Py_BUILD_CORE_BUILTIN",
]

_COMMON_INCLUDE_DIRS = [
    "Include/internal",
    "Objects",
    "Include",
    "Python",
    ".",
]

COMMON_COPTS = [
    "-std=c11"
]

expand_template(
    name = "modules_setup_bootstrap",
    template = "Modules/Setup.bootstrap.in",
    out = "Modules/Setup.bootstrap",
    substitutions = {
        "@MODULE_PWD_TRUE@": "",
    },
)

expand_template(
    name = "modules_setup_stdlib",
    template = "Modules/Setup.stdlib.in",
    out = "Modules/Setup.stdlib",
    substitutions = {
        "@MODULE_BUILDTYPE@": "shared",
        "@MODULE_ARRAY_TRUE@": "",
        "@MODULE__ASYNCIO_TRUE@": "",
        "@MODULE__BISECT_TRUE@": "",
        "@MODULE__CONTEXTVARS_TRUE@": "",
        "@MODULE__CSV_TRUE@": "",
        "@MODULE__HEAPQ_TRUE@": "",
        "@MODULE__JSON_TRUE@": "",
        "@MODULE__LSPROF_TRUE@": "",
        "@MODULE__OPCODE_TRUE@": "",
        "@MODULE__PICKLE_TRUE@": "",
        "@MODULE__QUEUE_TRUE@": "",
        "@MODULE__RANDOM_TRUE@": "",
        "@MODULE__STRUCT_TRUE@": "",
        "@MODULE__INTERPRETERS_TRUE@": "",
        "@MODULE__INTERPCHANNELS_TRUE@": "",
        "@MODULE__INTERPQUEUES_TRUE@": "",
        "@MODULE__ZONEINFO_TRUE@": "",
        "@MODULE_MATH_TRUE@": "",
        "@MODULE_CMATH_TRUE@": "",
        "@MODULE__STATISTICS_TRUE@": "",
        "@MODULE__DATETIME_TRUE@": "",
        "@MODULE__DECIMAL_TRUE@": "",
        "@MODULE_BINASCII_TRUE@": "",
        "@MODULE__BZ2_TRUE@": "",
        "@MODULE__LZMA_TRUE@": "",
        "@MODULE_ZLIB_TRUE@": "",
        "@MODULE__DBM_TRUE@": "#",
        "@MODULE__GDBM_TRUE@": "#",
        "@MODULE_READLINE_TRUE@": "#",
        "@MODULE__MD5_TRUE@": "",
        "@MODULE__SHA1_TRUE@": "",
        "@MODULE__SHA2_TRUE@": "",
        "@MODULE__SHA3_TRUE@": "",
        "@MODULE__BLAKE2_TRUE@": "",
        "@MODULE_PYEXPAT_TRUE@": "",
        "@MODULE__ELEMENTTREE_TRUE@": "",
        "@MODULE__CODECS_CN_TRUE@": "",
        "@MODULE__CODECS_HK_TRUE@": "",
        "@MODULE__CODECS_ISO2022_TRUE@": "",
        "@MODULE__CODECS_JP_TRUE@": "",
        "@MODULE__CODECS_KR_TRUE@": "",
        "@MODULE__CODECS_TW_TRUE@": "",
        "@MODULE__MULTIBYTECODEC_TRUE@": "",
        "@MODULE_UNICODEDATA_TRUE@": "",
        "@MODULE_FCNTL_TRUE@": "",
        "@MODULE_GRP_TRUE@": "",
        "@MODULE_MMAP_TRUE@": "",
        "@MODULE__POSIXSUBPROCESS_TRUE@": "",
        "@MODULE_RESOURCE_TRUE@": "",
        "@MODULE_SELECT_TRUE@": "",
        "@MODULE__SOCKET_TRUE@": "",
        "@MODULE_SYSLOG_TRUE@": "",
        "@MODULE_TERMIOS_TRUE@": "",
        "@MODULE__POSIXSHMEM_TRUE@": "",
        "@MODULE__MULTIPROCESSING_TRUE@": "",
        "@MODULE__CTYPES_TRUE@": "",
        "@MODULE__CURSES_TRUE@": "#",
        "@MODULE__CURSES_PANEL_TRUE@": "#",
        "@MODULE__SQLITE3_TRUE@": "",
        "@MODULE__SSL_TRUE@": "",
        "@MODULE__HASHLIB_TRUE@": "",
        "@MODULE__UUID_TRUE@": "#",
        "@MODULE__TKINTER_TRUE@": "",
        "@MODULE__SCPROXY_TRUE@": "#",
        "@MODULE_XXSUBTYPE_TRUE@": "",
        "@MODULE__XXTESTFUZZ_TRUE@": "",
        "@MODULE__TESTBUFFER_TRUE@": "",
        "@MODULE__TESTINTERNALCAPI_TRUE@": "",
        "@MODULE__TESTCAPI_TRUE@": "",
        "@MODULE__TESTLIMITEDCAPI_TRUE@": "",
        "@MODULE__TESTCLINIC_TRUE@": "",
        "@MODULE__TESTCLINIC_LIMITED_TRUE@": "",
        "@MODULE__TESTIMPORTMULTIPLE_TRUE@": "",
        "@MODULE__TESTMULTIPHASE_TRUE@": "",
        "@MODULE__TESTSINGLEPHASE_TRUE@": "",
        "@MODULE__TESTEXTERNALINSPECTION_TRUE@": "",
        "@MODULE__CTYPES_TEST_TRUE@": "",
        "@MODULE_XXLIMITED_TRUE@": "",
        "@MODULE_XXLIMITED_35_TRUE@": "",
        "@MODULE__CTYPES_MALLOC_CLOSURE@": "",
    },
)

genrule(
    name = "config_c",
    tools = ["Modules/makesetup"],
    srcs = [
        "Modules/config.c.in",
        ":modules_setup_bootstrap",
        ":modules_setup_stdlib",
    ],
    # -m - prevents the script from reading makefile.pre and generating a makefile
    cmd = "$(location Modules/makesetup) -c $(location Modules/config.c.in) -m - $(location :modules_setup_bootstrap) $(location :modules_setup_stdlib) && cp config.c $@",
    outs = ["config.c"],
)

filegroup(
    name = "parser_srcs",
    srcs = glob(["Parser/**/*.c"])
)

filegroup(
    name = "parser_headers",
    srcs = glob(["Parser/**/*.h"]) + [
        "Include/internal/pycore_parser.h",
    ]
)

filegroup(
    name = "python_srcs",
    srcs = glob(["Python/*.c"], exclude=[
        "Python/bytecodes.c",
        "Python/dup2.c",
        "Python/dynload*.c",
        "Python/emscripten_*.c",
        "Python/optimizer_bytecodes.c",
        "Python/frozen.c",
        "Python/sysmodule.c",
        "Python/getplatform.c",
    ]),
)

cc_library(
    name = "sysmodule_o",
    srcs = ["Python/sysmodule.c", ":python_headers", ":mimalloc_headers"],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = select({
        "@@//:linux_x86_64": ['MULTIARCH=\\"x86_64-linux-gnu\\"'],
        "@@//:linux_arm64": ['MULTIARCH=\\"aarch64-linux-gnu\\"'],
        # MULTIARCH is only defined for linux builds
        "//conditions:default": [],
    }) + [
        'ABIFLAGS=\\"\\"',
    ] + COMMON_DEFINES + PY_CORE_DEFINES,
    linkstatic = True,
)

cc_library(
    name = "getplatform_o",
    srcs = ["Python/getplatform.c", ":python_headers"],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = [
        'PLATFORM=\\"linux\\"',
    ] + COMMON_DEFINES + PY_CORE_DEFINES,
    linkstatic = True,
)

cc_library(
    name = "dynload_shlib",
    srcs = ["Python/dynload_shlib.c", ":python_headers"],
    local_defines = select({
        "@@//:linux_x86_64": ['SOABI=\\"cpython-313-x86_64-linux-gnu\\"'],
        "@@//:linux_arm64": ['SOABI=\\"cpython-313-aarch64-linux-gnu\\"'],
        "@platforms//os:macos": ['SOABI=\\"cpython-313-darwin\\"'],
    }) + COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    deps = [
        ":mimalloc",
    ],
    linkstatic = True,
)

filegroup(
    name = "object_srcs",
    srcs = glob(["Objects/*.c"]) + [
        "Objects/typeslots.inc",
    ] + select({
        "@platforms//os:linux": ["Python/asm_trampoline.S"],
        "//conditions:default": [],
    }) + glob(["Objects/*.h"])
)

filegroup(
    name = "modules_srcs",
    srcs = [
        ":config_c",
        "Modules/main.c",
        "Modules/gcmodule.c",
    ]
)

filegroup(
    name = "library_srcs_omit_frozen",
    srcs = [
        "Modules/getbuildinfo.c",
        ":parser_srcs",
        ":object_srcs",
        ":python_srcs",
        ":modules_srcs",
        ":sysmodule_o",
        ":getplatform_o",
        ":dynload_shlib",
    ] + PYTHON_CORE_MODULES_SRCS,
)

cc_library(
    name = "library_objs_omit_frozen",
    srcs = [":library_srcs_omit_frozen"],
    hdrs = [
        ":python_headers",
        ":modules_headers",
    ],
    deps = [":mimalloc"],
    includes = _COMMON_INCLUDE_DIRS,
    linkstatic = True,
    copts = COMMON_COPTS,
    local_defines = PY_CORE_DEFINES,
)

filegroup(
    name = "mimalloc_headers",
    srcs = glob(["Include/internal/mimalloc/**/*.h"])
)

cc_library(
    name = "mimalloc",
    srcs = [
        "Objects/obmalloc.c",
        "Include/internal/pycore_mimalloc.h",
        ":mimalloc_headers",
        ":python_headers",
    ],
    textual_hdrs = [
        "Objects/mimalloc/alloc.c",
        "Objects/mimalloc/alloc-aligned.c",
        "Objects/mimalloc/alloc-posix.c",
        "Objects/mimalloc/arena.c",
        "Objects/mimalloc/bitmap.c",
        "Objects/mimalloc/bitmap.h",
        "Objects/mimalloc/heap.c",
        "Objects/mimalloc/init.c",
        "Objects/mimalloc/options.c",
        "Objects/mimalloc/os.c",
        "Objects/mimalloc/page.c",
        "Objects/mimalloc/random.c",
        "Objects/mimalloc/segment.c",
        "Objects/mimalloc/segment-map.c",
        "Objects/mimalloc/stats.c",
        "Objects/mimalloc/prim/prim.c",
        # No need for a select here, the inclusion is conditionnal
        "Objects/mimalloc/prim/unix/prim.c",
        "Objects/mimalloc/prim/osx/prim.c",
        "Objects/mimalloc/page-queue.c",
        "Objects/mimalloc/alloc-override.c",
        "Objects/mimalloc/static.c",
    ],
    includes = _COMMON_INCLUDE_DIRS + [
        "Include/internal/mimalloc",
        "Objects/mimalloc",
    ],
    copts = COMMON_COPTS,
    local_defines = PY_CORE_DEFINES,
    linkstatic = True,
)

filegroup(
    name = "mpdec_headers",
    srcs = glob(["Modules/_decimal/libmpdec/*.h"]),
)

cc_library(
    name = "mpdec",
    hdrs = [":mpdec_headers"],
    srcs = glob(["Modules/_decimal/libmpdec/*.c"], exclude=[
        "Modules/_decimal/libmpdec/bench*.c",
        "Modules/_decimal/libmpdec/mpsignal.c"
    ]) + [":python_headers"],
    includes = ["Modules/_decimal/libmpdec"],
    copts = COMMON_COPTS,
    # These defines must be propagated to the dependents
    defines = [
        "CONFIG_64=1",
        "ANSI=1",
        "HAVE_UINT128_T=1",
    ],
    # Python's makefile explicitly links statically with libmpdec
    linkstatic = True,
)

cc_library(
    name = "hacl_sha2",
    srcs = [
        "Modules/_hacl/Hacl_Hash_SHA2.c",
        "Modules/_hacl/Hacl_Hash_SHA2.h",
        "Modules/_hacl/Hacl_Streaming_Types.h",
        "Modules/_hacl/internal/Hacl_Hash_SHA2.h",
        "Modules/_hacl/python_hacl_namespaces.h",
    ] + glob([
        "Modules/_hacl/include/krml/**/*.h"
    ]),
    includes = ["Modules/_hacl/include"],
    local_defines = [
        "_BSD_SOURCE",
        "_DEFAULT_SOURCE",
    ],
)


filegroup(
    name = "libexpat_srcs",
    srcs = glob([
        "Modules/expat/*.c",
        "Modules/expat/*.h",
    ], exclude=[
        "Modules/expat/xmltok_impl.c",
        "Modules/expat/xmltok_ns.c",
    ])
)

# Used from the modules list in modules.bzl
filegroup(
    name = "libexpat_textual_hdrs",
    srcs = [
        "Modules/expat/xmltok_impl.c",
        "Modules/expat/xmltok_ns.c",
    ]
)

# Used from the modules list in modules.bzl
filegroup(
    name = "blake2_hdrs",
    srcs = glob([
        "Modules/_blake2/impl/blake2*.h",
        "Modules/_blake2/impl/blake2*.c",
    ], exclude=[
        # These are the module srcs
        "Modules/_blake2/blake2module.c",
        "Modules/_blake2/blake2b_impl.c",
        "Modules/_blake2/blake2s_impl.c"
    ]),
)

python_freeze(
    name = "frozen_getpath_h",
    input = "Modules/getpath.py",
    module = "getpath",
    output = "Python/frozen_modules/getpath.h",
)

cc_library(
    name = "modules_getpath",
    srcs = [
        "Modules/getpath.c",
        ":python_headers",
        ":frozen_getpath_h",
    ],
    local_defines = [
        'PYTHONPATH=\\"\\"',
        'PREFIX=\\"/opt/datadog-agent/embedded\\"',
        'EXEC_PREFIX=\\"/opt/datadog-agent/embedded\\"',
        "VERSION=" + _VERSION,
        'VPATH=\\"..\\"',
        'PLATLIBDIR=\\"lib\\"',
        #FIXME this is not correct when building for macOS
        'PYTHONFRAMEWORK=\\"\\"',
    ] + COMMON_DEFINES + PY_CORE_DEFINES,
    copts = COMMON_COPTS,
    includes = _COMMON_INCLUDE_DIRS,
    deps = [
        ":mimalloc",
    ],
    linkstatic = True,
)

filegroup(
    name = "expat_headers",
    srcs = glob(["Modules/expat/*.h"]),
)

filegroup(
    name = "modules_headers",
    srcs = glob(["Modules/**/*.h"]),
)

filegroup(
    name = "public_headers",
    srcs = glob([
        "Include/*.h",
        "Include/internal/*.h",
        "Include/cpython/*.h",
    ])
)

filegroup(
    name = "python_headers",
    srcs = glob([
        "Objects/stringlib/**/*.h",
        "Objects/clinic/*.h",
        "Python/clinic/*.h",
        "Modules/clinic/*.h",
        "Python/*.h",
        "Include/*.h",
    ]) + [
        ":public_headers",
        ":copy_pyconfig",
        ":parser_headers",
    ]
)

cc_binary(
    name = "freeze_module",
    srcs = [
        "Programs/_freeze_module.c",
        "Modules/getpath_noop.c",
        ":python_headers",
        ":modules_headers",
        "Objects/typeslots.inc",
        ":library_objs_omit_frozen",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
        "//conditions:default": [],
    }),
    copts = COMMON_COPTS,
    deps = [
        ":mimalloc",
    ],# + [":module_" + m for m in PYTHON_MODULES.keys()],
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    visibility = ["@@//deps:__subpackages__"],
)

filegroup(
    name = "bootstrap_headers",
    srcs = [
        ":frozen_importlib_bootstrap_h",
        ":frozen_importlib_bootstrap_external_h",
        ":frozen_zipimport_h",
    ],
)

genrule(
    name = "frozen_importlib_bootstrap_h",
    srcs = ["Lib/importlib/_bootstrap.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/importlib._bootstrap.h"],
    cmd = "$(location :freeze_module) importlib._bootstrap $(location Lib/importlib/_bootstrap.py) $@"
)

genrule(
    name = "frozen_importlib_bootstrap_external_h",
    srcs = ["Lib/importlib/_bootstrap_external.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/importlib._bootstrap_external.h"],
    cmd = "$(location :freeze_module) importlib._bootstrap_external $(location Lib/importlib/_bootstrap_external.py) $@"
)

genrule(
    name = "frozen_zipimport_h",
    srcs = ["Lib/zipimport.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/zipimport.h"],
    cmd = "$(location :freeze_module) zipimport $(location Lib/zipimport.py) $@"
)

cc_binary(
    name = "bootstrap_python",
    srcs = [
        "Programs/_bootstrap_python.c",
        ":bootstrap_headers",
        ":python_headers",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
        "//conditions:default": [],
    }),
    deps = [
        ":modules_getpath",
        ":library_objs_omit_frozen",
        ":mimalloc",
    ] + [":build_module_" + m for m in PYTHON_MODULES.keys()],
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
)

# build_module_XXX represents the build definition of each module we need to build
# and include as a dep to the bootstrap python target.
# module_XXX represents the shared library that we need to install along the interpreter
[
    cc_library(
        name = "build_module_" + name,
        srcs = info['srcs'] + [":python_headers", ":modules_headers", ":expat_headers"] + info.get('extra_files', []),
        includes = _COMMON_INCLUDE_DIRS + info.get('includes', []),
        local_defines = COMMON_DEFINES + PY_BUILTIN_MODULE_DEFINES + info.get('local_defines', []),
        deps = [":mimalloc", ":libpython"] + info.get('deps', []),
        copts = COMMON_COPTS + info.get('extra_copts', []) +
            # Work around for macOS x86_64 where /usr/local/include is part of the
            # default headers locations which causes bazel to explicitly add "-I/usr/local/include"
            # to the command line.
            # By passing our openssl headers through copts it gets prepended and ensures our
            # openssl version takes precedence
            (select({
                "@@//:macos_x86_64": ["-Ibazel-out/darwin_x86_64-fastbuild/bin/external/+_repo_rules+openssl/openssl/include"],
                "//conditions:default": [""],
            }) if name in ["_ssl", "_hashlib"] else []),
        linkopts =
            (select({
                "@@//:macos_x86_64": ["-Lbazel-out/darwin_x86_64-fastbuild/bin/external/+_repo_rules+openssl/openssl/lib"],
                "//conditions:default": [""],
            }) if name in ["_ssl", "_hashlib"] else []),
        textual_hdrs = info.get('textual_hdrs', []),
    )
    for name, info in PYTHON_MODULES.items()
]

[
    cc_shared_library(
        name = "module_" + name,
        shared_lib_name = name + select({
            "@//:linux_arm64": ".cpython-313-aarch64-linux-gnu.so",
            "@//:linux_x86_64": ".cpython-313-x86_64-linux-gnu.so",
            "@platforms//os:macos": ".cpython-313-darwin.so",
        }),
        deps = ["build_module_" + name],
        dynamic_deps = info.get('dynamic_deps', []),
    )
    for name, info in PYTHON_MODULES.items()
]

[
    sh_test(
        name = "test_module_" + name,
        srcs = ["@@//deps:python3/test_module.sh"],
        args = ["$(location :python3)", name],
        data = [":python3"],
    )
    for name in PYTHON_MODULES.keys()
]

[
    python_freeze(
        name = "frozen_" + name,
        input = info['src'],
        module = name,
        output = info['output'],
    )
    for name, info in PYTHON_FROZEN_MODULES.items()
]

cc_library(
    name = "frozen_o",
    srcs = [
        "Python/frozen.c",
        ":python_headers",
        ":bootstrap_headers",
    ] + [":frozen_" + name for name in PYTHON_FROZEN_MODULES.keys()],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    linkstatic = True,
)

cc_library(
    name = "libpython",
    srcs = [
        ":python_headers",
        ":modules_headers",
    ],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    deps = [
        ":mimalloc",
        ":frozen_o",
        ":modules_getpath",
        ":library_objs_omit_frozen",
    ] + select({
        "@platforms//os:linux": [":dynload_shlib"],
        "@platforms//os:macos": [":dynload_shlib"],
    }),
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
        "//conditions:default": [],
    }),
    linkstatic = True,
)

cc_shared_library(
    name = "python" + _VERSION_FOR_SO,
    deps = [":libpython"],
    visibility = ["//visibility:public"],
)


cc_binary(
    name = "python3",
    srcs = [
        "Programs/python.c",
        ":python_headers",
    ],
    dynamic_deps = [
        ":python" + _VERSION_FOR_SO,
    ],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
)

so_symlink(
    name = "lib_files",
    libname = "libpython" + _VERSION_FOR_SO,
    src = ":python" + _VERSION_FOR_SO,
    version = "1.0",
)

# Headers need to be split by folders, as they are otherwise flattened to the same directory
# with some files having a conflicting name, which causes failures
pkg_files(
    name = "install_headers",
    srcs = [":public_headers", ":mimalloc_headers"],
    prefix = "include/python" + _VERSION_FOR_SO,
    strip_prefix = strip_prefix.from_root("Include/"),
)

pkg_files(
    name = "install_pyconfig_h",
    srcs = [":copy_pyconfig"],
    prefix = "include/python" + _VERSION_FOR_SO,
)

pkg_files(
    name = "bin_files",
    srcs = [":python3"],
    prefix = "bin",
    attributes = pkg_attributes("0755"),
)

pkg_files(
    name = "python_module_files",
    srcs = glob(["Lib/**/*.py"]),
    prefix = "lib/python" + _VERSION_FOR_SO,
    strip_prefix = strip_prefix.from_root("Lib/"),
)

pkg_files(
    name = "built_module_files",
    srcs = ["module_" + name for name in PYTHON_MODULES.keys()],
    prefix = "lib/python{}/lib-dynload".format(_VERSION_FOR_SO),
    strip_prefix = strip_prefix.from_root(),
)

pkg_files(
    name = "sysconfigdata_files",
    srcs = select({
        "@//:linux_arm64": ["_sysconfigdata__linux_aarch64-linux-gnu.py"],
        "@//:linux_x86_64": ["_sysconfigdata__linux_x86_64-linux-gnu.py"],
        "@//:macos_arm64": ["_sysconfigdata__darwin_aarch64_darwin.py"],
        "@//:macos_x86_64": ["_sysconfigdata__darwin_x86_64_darwin.py"],
    }),
    renames = select({
        "@//:macos_arm64": {"_sysconfigdata__darwin_aarch64_darwin.py": "_sysconfigdata__darwin_darwin.py"},
        "@//:macos_x86_64": {"_sysconfigdata__darwin_x86_64_darwin.py": "_sysconfigdata__darwin_darwin.py"},
        "//conditions:default": {},
    }),
    prefix = "lib/python" + _VERSION_FOR_SO,
)

pkg_files(
    name = "misc_module_files",
    srcs = glob(["Lib/ensurepip/_bundled/*.whl"]),
    prefix = "lib/python" + _VERSION_FOR_SO,
    strip_prefix = strip_prefix.from_root("Lib/"),
)

pkg_install(
    name = "install",
    srcs = [
        ":install_headers",
        ":python_module_files",
        ":built_module_files",
        ":lib_files",
        ":bin_files",
        ":sysconfigdata_files",
        ":misc_module_files",
        ":install_pyconfig_h",
    ],
)
