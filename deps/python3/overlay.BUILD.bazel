load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_shared_library.bzl", "cc_shared_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")
load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

load("@@//deps:python3/modules.bzl", "PYTHON_MODULES", "PYTHON_CORE_MODULES_SRCS", "PYTHON_FROZEN_MODULES")
load("@@//deps:python3/freeze.bzl", "python_freeze")

_VERSION = "3.13.7"

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:Python-2.0"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "copy_pyconfig",
    src = select({
        "@//:linux_arm64": "config-linux-arm64.h",
        "@//:linux_x86_64": "config-linux-x86_64.h",
        "@//:macos_arm64": "config-osx-arm64.h",
        "@//:macos_x86_64": "config-osx-x86_64.h",
    }),
    out = "pyconfig.h",  # minimize the number of exported include paths
)

COMMON_DEFINES = [
    "NDEBUG",
]

PY_CORE_DEFINES = [
    "Py_BUILD_CORE",
]

PY_BUILTIN_MODULE_DEFINES = [
    "Py_BUILD_CORE_BUILTIN",
]

_COMMON_INCLUDE_DIRS = [
    "Include/internal",
    "Objects",
    "Include",
    "Python",
    ".",
]

COMMON_COPTS = [
    "-std=c11"
]

expand_template(
    name = "modules_setup_bootstrap",
    template = "Modules/Setup.bootstrap.in",
    out = "Modules/Setup.bootstrap",
    substitutions = {
        "@MODULE_PWD_TRUE@": "",
    },
)

expand_template(
    name = "modules_setup_stdlib",
    template = "Modules/Setup.stdlib.in",
    out = "Modules/Setup.stdlib",
    substitutions = {
        "@MODULE_BUILDTYPE@": "shared",
        "@MODULE_ARRAY_TRUE@": "",
        "@MODULE__ASYNCIO_TRUE@": "",
        "@MODULE__BISECT_TRUE@": "",
        "@MODULE__CONTEXTVARS_TRUE@": "",
        "@MODULE__CSV_TRUE@": "",
        "@MODULE__HEAPQ_TRUE@": "",
        "@MODULE__JSON_TRUE@": "",
        "@MODULE__LSPROF_TRUE@": "",
        "@MODULE__OPCODE_TRUE@": "",
        "@MODULE__PICKLE_TRUE@": "",
        "@MODULE__QUEUE_TRUE@": "",
        "@MODULE__RANDOM_TRUE@": "",
        "@MODULE__STRUCT_TRUE@": "",
        "@MODULE__INTERPRETERS_TRUE@": "",
        "@MODULE__INTERPCHANNELS_TRUE@": "",
        "@MODULE__INTERPQUEUES_TRUE@": "",
        "@MODULE__ZONEINFO_TRUE@": "",
        "@MODULE_MATH_TRUE@": "",
        "@MODULE_CMATH_TRUE@": "",
        "@MODULE__STATISTICS_TRUE@": "",
        "@MODULE__DATETIME_TRUE@": "",
        "@MODULE__DECIMAL_TRUE@": "",
        "@MODULE_BINASCII_TRUE@": "",
        "@MODULE__BZ2_TRUE@": "",
        "@MODULE__LZMA_TRUE@": "",
        "@MODULE_ZLIB_TRUE@": "",
        "@MODULE__DBM_TRUE@": "#",
        "@MODULE__GDBM_TRUE@": "#",
        "@MODULE_READLINE_TRUE@": "#",
        "@MODULE__MD5_TRUE@": "",
        "@MODULE__SHA1_TRUE@": "",
        "@MODULE__SHA2_TRUE@": "",
        "@MODULE__SHA3_TRUE@": "",
        "@MODULE__BLAKE2_TRUE@": "",
        "@MODULE_PYEXPAT_TRUE@": "",
        "@MODULE__ELEMENTTREE_TRUE@": "",
        "@MODULE__CODECS_CN_TRUE@": "",
        "@MODULE__CODECS_HK_TRUE@": "",
        "@MODULE__CODECS_ISO2022_TRUE@": "",
        "@MODULE__CODECS_JP_TRUE@": "",
        "@MODULE__CODECS_KR_TRUE@": "",
        "@MODULE__CODECS_TW_TRUE@": "",
        "@MODULE__MULTIBYTECODEC_TRUE@": "",
        "@MODULE_UNICODEDATA_TRUE@": "",
        "@MODULE_FCNTL_TRUE@": "",
        "@MODULE_GRP_TRUE@": "",
        "@MODULE_MMAP_TRUE@": "",
        "@MODULE__POSIXSUBPROCESS_TRUE@": "",
        "@MODULE_RESOURCE_TRUE@": "",
        "@MODULE_SELECT_TRUE@": "",
        "@MODULE__SOCKET_TRUE@": "",
        "@MODULE_SYSLOG_TRUE@": "",
        "@MODULE_TERMIOS_TRUE@": "",
        "@MODULE__POSIXSHMEM_TRUE@": "",
        "@MODULE__MULTIPROCESSING_TRUE@": "",
        "@MODULE__CTYPES_TRUE@": "",
        "@MODULE__CURSES_TRUE@": "#",
        "@MODULE__CURSES_PANEL_TRUE@": "#",
        "@MODULE__SQLITE3_TRUE@": "",
        "@MODULE__SSL_TRUE@": "",
        "@MODULE__HASHLIB_TRUE@": "",
        "@MODULE__UUID_TRUE@": "#",
        "@MODULE__TKINTER_TRUE@": "",
        "@MODULE__SCPROXY_TRUE@": "#",
        "@MODULE_XXSUBTYPE_TRUE@": "",
        "@MODULE__XXTESTFUZZ_TRUE@": "",
        "@MODULE__TESTBUFFER_TRUE@": "",
        "@MODULE__TESTINTERNALCAPI_TRUE@": "",
        "@MODULE__TESTCAPI_TRUE@": "",
        "@MODULE__TESTLIMITEDCAPI_TRUE@": "",
        "@MODULE__TESTCLINIC_TRUE@": "",
        "@MODULE__TESTCLINIC_LIMITED_TRUE@": "",
        "@MODULE__TESTIMPORTMULTIPLE_TRUE@": "",
        "@MODULE__TESTMULTIPHASE_TRUE@": "",
        "@MODULE__TESTSINGLEPHASE_TRUE@": "",
        "@MODULE__TESTEXTERNALINSPECTION_TRUE@": "",
        "@MODULE__CTYPES_TEST_TRUE@": "",
        "@MODULE_XXLIMITED_TRUE@": "",
        "@MODULE_XXLIMITED_35_TRUE@": "",
        "@MODULE__CTYPES_MALLOC_CLOSURE@": "",
    },
)

genrule(
    name = "config_c",
    tools = ["Modules/makesetup"],
    srcs = [
        "Modules/config.c.in",
        ":modules_setup_bootstrap",
        ":modules_setup_stdlib",
    ],
    # -m - prevents the script from reading makefile.pre and generating a makefile
    cmd = "$(location Modules/makesetup) -c $(location Modules/config.c.in) -m - $(location :modules_setup_bootstrap) $(location :modules_setup_stdlib) && cp config.c $@",
    outs = ["config.c"],
)

filegroup(
    name = "parser_srcs",
    srcs = glob(["Parser/**/*.c"])
)

filegroup(
    name = "parser_headers",
    srcs = glob(["Parser/**/*.h"]) + [
        "Include/internal/pycore_parser.h",
    ]
)

filegroup(
    name = "python_srcs",
    srcs = glob(["Python/*.c"], exclude=[
        "Python/bytecodes.c",
        "Python/dup2.c",
        "Python/dynload*.c",
        "Python/emscripten_*.c",
        "Python/optimizer_bytecodes.c",
        "Python/frozen.c",
    ])
)

cc_library(
    name = "dynload_shlib",
    srcs = ["Python/dynload_shlib.c", ":python_headers"],
    local_defines = select({
        "@@//:linux_x86_64": ['SOABI=\\"cpython-315-x86_64-linux-gnu\\"'],
        "@@//:linux_arm64": ['SOABI=\\"cpython-315-aarch64-linux-gnu\\"'],
    }) + COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    deps = [
        ":mimalloc",
    ]

)

filegroup(
    name = "object_srcs",
    srcs = glob(["Objects/*.c"]) + [
        "Objects/typeslots.inc",
    ] + select({
        "@platforms//os:linux": ["Python/asm_trampoline.S"],
        "//conditions:default": [],
    }) + glob(["Objects/*.h"])
)

filegroup(
    name = "modules_srcs",
    srcs = [
        ":config_c",
        "Modules/main.c",
        "Modules/gcmodule.c",
    ]
)

filegroup(
    name = "library_srcs_omit_frozen",
    srcs = [
        "Modules/getbuildinfo.c",
        ":parser_srcs",
        ":object_srcs",
        ":python_srcs",
        ":modules_srcs",
    ] + PYTHON_CORE_MODULES_SRCS,
)

filegroup(
    name = "mimalloc_headers",
    srcs = [
        "Include/internal/pycore_mimalloc.h",
        "Include/internal/mimalloc/mimalloc.h",
        "Include/internal/mimalloc/mimalloc/atomic.h",
        "Include/internal/mimalloc/mimalloc/internal.h",
        "Include/internal/mimalloc/mimalloc/prim.h",
        "Include/internal/mimalloc/mimalloc/track.h",
        "Include/internal/mimalloc/mimalloc/types.h",
    ]
)

cc_library(
    name = "mimalloc",
    srcs = [
        "Objects/obmalloc.c",
        ":mimalloc_headers",
        ":python_headers",
    ],
    textual_hdrs = [
        "Objects/mimalloc/alloc.c",
        "Objects/mimalloc/alloc-aligned.c",
        "Objects/mimalloc/alloc-posix.c",
        "Objects/mimalloc/arena.c",
        "Objects/mimalloc/bitmap.c",
        "Objects/mimalloc/bitmap.h",
        "Objects/mimalloc/heap.c",
        "Objects/mimalloc/init.c",
        "Objects/mimalloc/options.c",
        "Objects/mimalloc/os.c",
        "Objects/mimalloc/page.c",
        "Objects/mimalloc/random.c",
        "Objects/mimalloc/segment.c",
        "Objects/mimalloc/segment-map.c",
        "Objects/mimalloc/stats.c",
        "Objects/mimalloc/prim/prim.c",
        # No need for a select here, the inclusion is conditionnal
        "Objects/mimalloc/prim/unix/prim.c",
        "Objects/mimalloc/prim/osx/prim.c",
        "Objects/mimalloc/page-queue.c",
        "Objects/mimalloc/alloc-override.c",
        "Objects/mimalloc/static.c",
    ],
    includes = _COMMON_INCLUDE_DIRS + [
        "Include/internal/mimalloc",
        "Objects/mimalloc",
    ],
    local_defines = PY_CORE_DEFINES
)

filegroup(
    name = "mpdec_headers",
    srcs = glob(["Modules/_decimal/libmpdec/*.h"]),
)

cc_library(
    name = "mpdec",
    hdrs = [":mpdec_headers"],
    # srcs = glob(["Modules/_decimal/libmpdec/*.c"], exclude=[
    #     "Modules/_decimal/libmpdec/bench*.c",
    #     "Modules/_decimal/libmpdec/mpsignal.c"
    # ]) + [":python_headers"],
    srcs = [
        "Modules/_decimal/libmpdec/basearith.c",
        "Modules/_decimal/libmpdec/constants.c",
        "Modules/_decimal/libmpdec/context.c",
        "Modules/_decimal/libmpdec/convolute.c",
        "Modules/_decimal/libmpdec/crt.c",
        "Modules/_decimal/libmpdec/difradix2.c",
        "Modules/_decimal/libmpdec/fnt.c",
        "Modules/_decimal/libmpdec/fourstep.c",
        "Modules/_decimal/libmpdec/io.c",
        "Modules/_decimal/libmpdec/mpalloc.c",
        "Modules/_decimal/libmpdec/mpdecimal.c",
        "Modules/_decimal/libmpdec/numbertheory.c",
        "Modules/_decimal/libmpdec/sixstep.c",
        "Modules/_decimal/libmpdec/transpose.c",
        ":python_headers",
    ],
    includes = ["Modules/_decimal/libmpdec"],
    # These defines must be propagated to the dependents
    defines = [
        "CONFIG_64=1",
        "ANSI=1",
        "HAVE_UINT128_T=1",
    ],
    # Python's makefile explicitly links statically with libmpdec
    linkstatic = True,
)

filegroup(
    name = "libexpat_srcs",
    srcs = glob([
        "Modules/expat/*.c",
        "Modules/expat/*.h",
    ], exclude=[
        "Modules/expat/xmltok_impl.c",
        "Modules/expat/xmltok_ns.c",
    ])
)

filegroup(
    name = "libexpat_textual_hdrs",
    srcs = glob([
        "Modules/expat/xmltok_impl.c",
        "Modules/expat/xmltok_ns.c",
    ])
)

filegroup(
    name = "blake2_hdrs",
    srcs = glob([
        "Modules/_blake2/impl/blake2*.h",
        "Modules/_blake2/impl/blake2*.c",
    ], exclude=[
        # These are the module srcs
        "Modules/_blake2/blake2module.c",
        "Modules/_blake2/blake2b_impl.c",
        "Modules/_blake2/blake2s_impl.c"
    ]),
)

python_freeze(
    name = "frozen_getpath_h",
    input = "Modules/getpath.py",
    module = "getpath",
    output = "Python/frozen_modules/getpath.h",
)

cc_library(
    name = "modules_getpath",
    srcs = [
        "Modules/getpath.c",
        ":python_headers",
        ":frozen_getpath_h",
    ],
    local_defines = [
        'PYTHONPATH=\\"\\"',
        'PREFIX=\\"/opt/datadog-agent/embedded\\"',
        'EXEC_PREFIX=\\"/opt/datadog-agent/embedded\\"',
        "VERSION=" + _VERSION,
        'VPATH=\\"..\\"',
        'PLATLIBDIR=\\"lib\\"',
        #FIXME this is not correct when building for macOS
        'PYTHONFRAMEWORK=\\"\\"',
    ] + COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    deps = [
        ":mimalloc",
    ]
)

filegroup(
    name = "library_srcs",
    srcs = [
        ":library_srcs_omit_frozen",
        "Python/frozen.c",
    ]
)

filegroup(
    name = "bytestr_headers",
    srcs = [
        "Objects/stringlib/count.h",
        "Objects/stringlib/ctype.h",
        "Objects/stringlib/fastsearch.h",
        "Objects/stringlib/find.h",
        "Objects/stringlib/join.h",
        "Objects/stringlib/partition.h",
        "Objects/stringlib/split.h",
        "Objects/stringlib/stringdefs.h",
        "Objects/stringlib/transmogrify.h",
    ],
)

filegroup(
    name = "unicode_headers",
    srcs = [
        "Objects/stringlib/asciilib.h",
        "Objects/stringlib/codecs.h",
        "Objects/stringlib/count.h",
        "Objects/stringlib/fastsearch.h",
        "Objects/stringlib/find.h",
        "Objects/stringlib/find_max_char.h",
        "Objects/stringlib/localeutil.h",
        "Objects/stringlib/partition.h",
        "Objects/stringlib/replace.h",
        "Objects/stringlib/split.h",
        "Objects/stringlib/ucs1lib.h",
        "Objects/stringlib/ucs2lib.h",
        "Objects/stringlib/ucs4lib.h",
        "Objects/stringlib/undef.h",
        "Objects/stringlib/unicode_format.h",
    ],
)

filegroup(
    name = "stringlib_other_headers",
    srcs = [
        "Objects/stringlib/eq.h",
    ]
)

filegroup(
    name = "stringlib_clinic_headers",
    srcs = glob(["Objects/stringlib/clinic/*.h"]),
)

filegroup(
    name = "objects_clinic_headers",
    srcs = glob(["Objects/clinic/*.h"]),
)

filegroup(
    name = "python_clinic_headers",
    srcs = glob(["Python/clinic/*.h"]),
)

filegroup(
    name = "modules_clinic_headers",
    srcs = glob(["Modules/clinic/*.h"]),
)

filegroup(
    name = "expat_headers",
    srcs = glob(["Modules/expat/*.h"]),
)

filegroup(
    name = "modules_headers",
    srcs = glob(["Modules/**/*.h"]),
)

filegroup(
    name = "python_headers",
    srcs = glob([
        "Include/*.h",
        "Include/cpython/*.h",
        "Include/internal/*.h",
        "Python/*.h",
    ]) + [
        ":copy_pyconfig",
        ":pegen_headers",
        ":tokenizer_headers",
        ":parser_headers",
        ":bytestr_headers",
        ":unicode_headers",
        ":objects_clinic_headers",
        ":stringlib_clinic_headers",
        ":stringlib_other_headers",
        ":python_clinic_headers",
        ":modules_clinic_headers",
    ]
)

cc_binary(
    name = "freeze_module",
    srcs = [
        "Programs/_freeze_module.c",
        "Modules/getpath_noop.c",
        ":python_headers",
        ":library_srcs_omit_frozen",
        "Objects/typeslots.inc",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
    }),
    copts = COMMON_COPTS,
    deps = select({
        "@platforms//os:linux": [":dynload_shlib"],
    }) + [
        ":mimalloc",
    ] + [":module_" + m for m in PYTHON_MODULES.keys()],
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    visibility = ["@@//deps:__subpackages__"],
)

filegroup(
    name = "bootstrap_headers",
    srcs = [
        ":frozen_importlib_bootstrap_h",
        ":frozen_importlib_bootstrap_external_h",
        ":frozen_zipimport_h",
    ],
)

genrule(
    name = "frozen_importlib_bootstrap_h",
    srcs = ["Lib/importlib/_bootstrap.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/importlib._bootstrap.h"],
    cmd = "$(location :freeze_module) importlib._bootstrap $(location Lib/importlib/_bootstrap.py) $@"
)

genrule(
    name = "frozen_importlib_bootstrap_external_h",
    srcs = ["Lib/importlib/_bootstrap_external.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/importlib._bootstrap_external.h"],
    cmd = "$(location :freeze_module) importlib._bootstrap_external $(location Lib/importlib/_bootstrap_external.py) $@"
)

genrule(
    name = "frozen_zipimport_h",
    srcs = ["Lib/zipimport.py"],
    tools = [":freeze_module"],
    outs = ["Python/frozen_modules/zipimport.h"],
    cmd = "$(location :freeze_module) zipimport $(location Lib/zipimport.py) $@"
)

cc_binary(
    name = "bootstrap_python",
    srcs = [
        "Programs/_bootstrap_python.c",
        ":library_srcs_omit_frozen",
        ":modules_getpath",
        ":bootstrap_headers",
        ":python_headers",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
    }),
    deps = select({
        "@platforms//os:linux": [":dynload_shlib"],
    }) + [
        ":mimalloc",
    ] + [":module_" + m for m in PYTHON_MODULES.keys()],
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
)

[
        cc_binary(
            name = "module_" + name,
            srcs = info['srcs'] + [":python_headers", ":modules_headers", ":expat_headers"] + info.get('extra_files', []),
            includes = _COMMON_INCLUDE_DIRS + info.get('includes', []),
            local_defines = COMMON_DEFINES + PY_BUILTIN_MODULE_DEFINES + info.get('local_defines', []),
            deps = [":mimalloc"] + info.get('deps', []),
            copts = COMMON_COPTS,
            linkshared=True,
        )
    if info.get('force_cc_binary', "no") == "yes" else
        cc_library(
            name = "module_" + name,
            srcs = info['srcs'] + [":python_headers", ":modules_headers", ":expat_headers"] + info.get('extra_files', []),
            includes = _COMMON_INCLUDE_DIRS + info.get('includes', []),
            local_defines = COMMON_DEFINES + PY_BUILTIN_MODULE_DEFINES + info.get('local_defines', []),
            deps = [":mimalloc"] + info.get('deps', []),
            copts = COMMON_COPTS,
            textual_hdrs = info.get('textual_hdrs', []),
        )
    for name, info in PYTHON_MODULES.items()
]

[
    python_freeze(
        name = "frozen_" + name,
        input = info['src'],
        module = name,
        output = info['output'],
    )
    for name, info in PYTHON_FROZEN_MODULES.items()
]

cc_library(
    name = "frozen_o",
    srcs = [
        "Python/frozen.c",
        ":python_headers",
        ":bootstrap_headers",
    ] + [":frozen_" + name for name in PYTHON_FROZEN_MODULES.keys()],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
)

cc_library(
    name = "libpython",
    srcs = [
        ":library_srcs_omit_frozen",
        ":modules_getpath",
        ":frozen_o",
        ":python_headers",
        ":modules_headers",
    ],
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    deps = [
        ":mimalloc",
    ]
)

cc_binary(
    name = "python",
    srcs = [
        "Programs/python.c",
        ":dynload_shlib",
        ":libpython",
        ":modules_getpath",
        ":frozen_o",
    ],
    data = glob(["Lib/**/*.py"]),
    includes = _COMMON_INCLUDE_DIRS,
    copts = COMMON_COPTS,
    local_defines = COMMON_DEFINES + PY_CORE_DEFINES,
    deps = [
        ":mimalloc",
    ],
    linkopts = select({
        "@platforms//os:linux": ["-lutil"],
    }),

)
