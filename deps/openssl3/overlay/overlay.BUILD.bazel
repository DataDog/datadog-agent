# Agent specific openssl3 build.

load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_license//rules:license.bzl", "license")
load(
    "darwin_arm64_lib_contents.bzl",
    "HDRS",
    "HDRS_DARWIN_ARM64",
    "LIB_CRYPTO_SRCS_DARWIN_ARM64",
    "TEXTUAL_HEADERS",
)

package(
    default_applicable_licenses = [":license"],
    default_visibility = ["//visibility:private"],
)

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:Apache-2.0"],
    license_text = "LICENSE.txt",
    visibility = ["//visibility:public"],
)

exports_files([
    "LICENSE.txt",
])

VERSION = "3.5.2"
SONAME = "libcrypto.so.%s" % VERSION

# These are found by examining the output of configure / make
LOCAL_DEFINES = [
    # do not sort
    "OPENSSLDIR='\"/usr/local/ssl\"'",
    "ENGINESDIR='\"/usr/local/lib/engines-3\"'",
    "MODULESDIR='\"/usr/local/lib/ossl-modules\"'",
    "_REENTRANT",
    "OPENSSL_CPUID_OBJ",
    "L_ENDIAN",
    "OPENSSL_PIC",
    "ZLIB",
]

DARWIN_ARM64_DEFINES = [
    "BSAES_ASM",
    "ECP_NISTZ256_ASM",
    "ECP_SM2P256_ASM",
    "KECCAK1600_ASM",
    "MD5_ASM",
    "OPENSSL_BN_ASM_MONT",
    "OPENSSL_SM3_ASM",
    "POLY1305_ASM",
    "SHA1_ASM",
    "SHA256_ASM",
    "SHA512_ASM",
    "SM4_ASM",
    "VPAES_ASM",
    "VPSM4_ASM",
]

cc_library(
    name = "libcrypto",
    srcs = HDRS + select({
        # TODO: This needs to be arch specific too.
        "@platforms//os:macos": HDRS_DARWIN_ARM64 + LIB_CRYPTO_SRCS_DARWIN_ARM64,
    }),
    # Yeech.  Includes polutes the build. But we can not use -I because we don't know
    # the path to the folder when building via external/openssl3/...
    # Ideal solution:
    #    port local_incudes concept from Google out to rules_cc.
    includes = [
        "include",
        "crypto",
        "openssl",
        "providers",
        "providers/common/include",
        "providers/fips/include",
        "providers/implementations/include",
    ] + select({
        # TODO: This needs to be arch specific too.
        "@platforms//os:linux": [
            "linux_arm64/crypto",
            "linux_arm64/include",
            "linux_arm64/providers/common/include",
        ],
        "@platforms//os:macos": [
            "darwin_arm64/crypto",
            "darwin_arm64/include",
            "darwin_arm64/providers/common/include",
        ],
    }),
    local_defines = LOCAL_DEFINES + select({
        "@platforms//os:macos": DARWIN_ARM64_DEFINES,
    }),
    copts = select({
        "@platforms//os:linux": [
            "-fPIC",
            # O3 and NDEBUG a pair you should consider together, that is why
            # it is a copt rather than define.
            "-DNDEBUG",
            "-O3",
        ],
        "@platforms//os:macos": [
            "-fPIC",
            "-DNDEBUG",
            "-O3",
        ],
    }),
    textual_hdrs = TEXTUAL_HEADERS + glob(["**/*.inc"]),
    visibility = ["//visibility:public"],
    deps = [
        "@zlib//:zlib",
    ],
)
