load("@rules_license//rules:license.bzl", "license")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@@//bazel/rules:so_symlink.bzl", "so_symlink")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_shared_library")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")

_VERSION = "2.5.1"

license(
    name = "license",
    license_kinds = ["@rules_license//licenses/spdx:LGPL-2.1"],
    license_text = "doc/COPYING.LGPL",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "copy_config_h",
    src = select({
        "@@//:linux_x86_64": "config-linux-x86_64.h",
        "@@//:linux_arm64": "config-linux-arm64.h",
    }),
    out = "config.h",
)

_PUBLIC_HEADERS = [
    "include/libattr.h",
    "include/attributes.h",
    "include/error_context.h",
]

cc_library(
    name = "libattr",
    srcs = [
        ":copy_config_h",
        "libattr/attr_copy_action.c",
        "libattr/attr_copy_check.c",
        "libattr/attr_copy_fd.c",
        "libattr/attr_copy_file.c",
        "libattr/libattr.c",
        "libattr/syscalls.c",
        "libattr/libattr.h",
        "include/nls.h",
    ],
    hdrs = _PUBLIC_HEADERS,
    includes = [
        "include",
    ],
    include_prefix = "attr",
    strip_include_prefix = "include",
    copts = [
        "-include $(location libattr/libattr.h)"
    ],
    local_defines = [
        "HAVE_CONFIG_H",
        'SYSCONFDIR=\\"/opt/datadog-agent/embedded/etc\\"',
    ]
)

cc_shared_library(
    name = "attr",
    deps = [":libattr"],
    additional_linker_inputs = [
        "exports",
    ],
    user_link_flags = [
        "-Wl,--version-script=$(location exports)",
    ],
)

so_symlink(
    name = "lib_files",
    src = ":attr",
    libname = "libattr",
    version = "1.1.2501",
)

pkg_files(
    name = "hdr_files",
    srcs = _PUBLIC_HEADERS,
    prefix = "include/attr/",
)

expand_template(
    name = "gen_pkgconfig",
    out = "libattr.pc",
    substitutions = {
        # Prepare the .pc file for our own substitution later
        "@prefix@": "##PREFIX##",
        "@exec_prefix@": "${prefix}",
        "@libdir@": "${prefix}/lib",
        "@includedir@": "${prefix}/include",
        "@PACKAGE_VERSION@": _VERSION,
    },
    template = "libattr.pc.in",
)

pkg_files(
    name = "pc_file",
    srcs = [":gen_pkgconfig"],
    prefix = "lib/pkgconfig",
)

pkg_install(
    name = "install",
    srcs = [
        ":hdr_files",
        ":lib_files",
        ":pc_file",
    ],
)
