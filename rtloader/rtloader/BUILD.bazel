load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_shared_library.bzl", "cc_shared_library")
load("//bazel/rules:dd_agent_expand_template.bzl", "dd_agent_expand_template")
load("//bazel/rules:so_symlink.bzl", "so_symlink")
load("//rtloader:defs.bzl", "COMMON_DEFINES")

package(default_visibility = ["//rtloader:__pkg__"])

# This package uses a distinct compatibility level. On linux we see:
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so -> libdatadog-agent-rtloader.so.2
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so.0.1.0
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so.2 -> libdatadog-agent-rtloader.so.0.1.0
# On mac
# $ otool -L /opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.0.1.0.dylib
# /opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.0.1.0.dylib:
#	@rpath/libdatadog-agent-rtloader.2.dylib (compatibility version 2.0.0, current version 0.1.0)
#	/usr/lib/libc++.1.dylib (compatibility version 1.0.0, current version 1800.105.0)
#	/usr/lib/libSystem.B.dylib (compatibility version 1.0.0, current version 1351.0.0)

# This may take changes to so_symlink. That should be a distint PR.
VERSION = "0.1.0"

# SO version is ahead of the actual version because we bumped SO
# when we dropped python 2 support.
COMPATIBILITY_VERSION = "2"

dd_agent_expand_template(
    name = "pc_gen",
    out = "datadog-agent-rtloader.pc",
    substitutions = {
        "@CMAKE_INSTALL_PREFIX@": "{install_dir}/embedded",
        "@CMAKE_INSTALL_LIBDIR@": "lib",
        "@CMAKE_INSTALL_INCLUDEDIR@": "include",
        "@PROJECT_NAME@": "datadog-agent-rtloader",
        "@PROJECT_VERSION@": "0.1.0",
        "@PROJECT_DESCRIPTION@": "CPython backend for the Datadog Agent",
    },
    template = "datadog-agent-rtloader.pc.in",
)

cc_library(
    name = "static",
    srcs = [
        "api.cpp",
        "rtloader.cpp",
        "//rtloader/common:rtloader_mem.c",
    ],
    copts = select({
        "@platforms//os:windows": [
            # Static library on windows so we don't have to distribute CRT.
            "/MT",
        ],
        "//conditions:default": ["-O2"],
    }),
    includes = ["."],
    local_defines = COMMON_DEFINES + select({
        "@platforms//os:windows": [
            "_hypot=hypot",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//rtloader:__subpackages__"],
    deps = [
        "//rtloader/common:headers",
        "//rtloader/include",
    ],
)

cc_shared_library(
    name = "datadog-agent-rtloader",
    user_link_flags = select({
        "@platforms//os:linux": [
            "-Wl,-soname,datadog-agent-rtloader.so.%s" % VERSION,
        ],
        "@platforms//os:macos": [
            "-Wl,-install_name,datadog-agent-rtloader.%s.dylib" % VERSION,
            "-Wl,-current_version,%s" % VERSION,
            "-Wl,-compatibility_version,%s" % COMPATIBILITY_VERSION,
        ],
        "@platforms//os:windows": [
            # Static library on windows so we don't have to distribute CRT.
            "/MT",
        ],
    }),
    deps = [":static"],
)

# TODO: Need to get the linux ones right
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so -> libdatadog-agent-rtloader.so.2
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so.0.1.0
#  ./opt/datadog-agent/embedded/lib/libdatadog-agent-rtloader.so.2 -> libdatadog-agent-rtloader.so.0.1.0

so_symlink(
    name = "lib_files",
    src = ":datadog-agent-rtloader",
    libname = "libdatadog-agent-rtloader",
    prefix = "embedded",
    version = COMPATIBILITY_VERSION,
)

# buildifier: disable=no-effect
"""
Remaining work:

# https://cmake.org/cmake/help/latest/module/FindBacktrace.html
find_package(Backtrace)
if(Backtrace_FOUND)
    if(NOT TARGET Backtrace::Backtrace)
        # for pre-3.30 CMake versions
        add_library(Backtrace::Backtrace INTERFACE IMPORTED)
        set_target_properties(Backtrace::Backtrace PROPERTIES
            INTERFACE_LINK_LIBRARIES "${Backtrace_LIBRARIES}"
            INTERFACE_INCLUDE_DIRECTORIES "${Backtrace_INCLUDE_DIRS}"
        )
    endif()
    target_link_libraries(datadog-agent-rtloader PRIVATE Backtrace::Backtrace)
    target_compile_definitions(datadog-agent-rtloader PRIVATE HAS_BACKTRACE_LIB)
endif()
endif()

-- probably not needed, -m32 comes from the toolchain
if(ARCH_I386)
    set_target_properties(datadog-agent-rtloader PROPERTIES COMPILE_FLAGS "-m32" LINK_FLAGS "-m32")
endif()

install(TARGETS datadog-agent-rtloader
    RUNTIME DESTINATION bin
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    PUBLIC_HEADER DESTINATION include
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

# Windows builder assumes libdatadog-agent-rtloader.a is located in rtloader/rtloader folder.
if (WIN32)
ADD_CUSTOM_COMMAND(
    TARGET datadog-agent-rtloader
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy ${PROJECT_BINARY_DIR}/libdatadog-agent-rtloader.a ${PROJECT_SOURCE_DIR}/libdatadog-agent-rtloader.a
)
endif()
"""
