# Foundation packages

bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "package_metadata", version = "0.0.5")
bazel_dep(name = "bazel_features", version = "1.34.0")
bazel_dep(name = "bazel_skylib", version = "1.8.1")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_pkg", version = "1.1.0")

# Temporary until a future rules_pkg release
git_override(
    module_name = "rules_pkg",
    commit = "4eed5466fb7062e196c4fa01495e1dfe0cf5c850",
    remote = "https://github.com/bazelbuild/rules_pkg",
)

#########################
## Prebuilt binaries   ##
#########################

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "com_github_bazelbuild_buildtools",
    patch_strip = 1,
    patches = [
        "//bazel/patches:buildifier-build.patch",  # https://github.com/bazelbuild/buildtools/pull/1398
        "//bazel/patches:buildifier-internal-factory.patch",  # https://github.com/bazelbuild/buildtools/pull/1399
        "//bazel/patches:buildifier-runner-bat-template.patch",  # ^ https://github.com/bazelbuild/buildtools/pull/1400
    ],
    sha256 = "53119397bbce1cd7e4c590e117dcda343c2086199de62932106c80733526c261",
    strip_prefix = "buildtools-8.2.1",
    urls = ["https://github.com/bazelbuild/buildtools/archive/refs/tags/v8.2.1.tar.gz"],
)

bazel_dep(name = "rules_multitool", version = "1.9.0")

multitool = use_extension("@rules_multitool//multitool:extension.bzl", "multitool")
multitool.hub(lockfile = "//bazel:prebuilt_buildtools.json")
multitool.hub(lockfile = "//bazel:prebuilt_jq.json")
use_repo(multitool, "multitool")

######################################
## Compilers and toolchains        ##
####################################

bazel_dep(name = "rules_cc", version = "0.2.8")
bazel_dep(name = "rules_python", version = "1.6.1")
bazel_dep(name = "gcc_toolchain", version = "0.9.0")
git_override(
    module_name = "gcc_toolchain",
    commit = "0e2242b07961107d2b05a6552d70b2ef4ef485fb",
    patch_args = ["-p1"],
    patches = ["//bazel/patches:datadog_agent_toolchain.patch"],
    remote = "https://github.com/f0rmiga/gcc-toolchain.git",
)

gcc_toolchains = use_extension("@gcc_toolchain//toolchain:module_extensions.bzl", "gcc_toolchains", dev_dependency = True)
gcc_toolchains.toolchain(
    name = "gcc_toolchain_x86_64",
    binary_prefix = "x86_64-unknown-linux-gnu-",
    gcc_version = "11.4.0",
    target_arch = "x86_64",
)
gcc_toolchains.toolchain(
    name = "gcc_toolchain_aarch64",
    binary_prefix = "aarch64-unknown-linux-gnu-",
    gcc_version = "12.3.0",
    target_arch = "aarch64",
)
use_repo(gcc_toolchains, "gcc_toolchain_x86_64")
use_repo(gcc_toolchains, "gcc_toolchain_aarch64")

# TODO{agent-build}: Find a way to register platform-specific toolchains dynamically
register_toolchains(
    "@gcc_toolchain_x86_64//:cc_toolchain",
    "@gcc_toolchain_aarch64//:cc_toolchain",
    "//bazel/toolchains/mingw:mingw_cc_toolchain",
)

#########################
## Native dependencies ##
#########################

# Make our 3rd parties depencency repositories known
include("//deps:repos.MODULE.bazel")
