"""Agent package components specific to linux."""

load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load(
    "@rules_pkg//pkg:mappings.bzl",
    "pkg_attributes",
    "pkg_filegroup",
    "pkg_mkdirs",
)
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//bazel/rules:dd_agent_pkg_mklink.bzl", "dd_agent_pkg_mklink")
load("//compliance:package_licenses.bzl", "package_licenses")

package(default_visibility = ["//packages:__subpackages__"])

# This is everything in the distro. All the things that were explicit
# in datadog-agent-dependencies.rb should show up here.
pkg_filegroup(
    name = "agent_components",
    srcs = [
        # TODO: remove jmxfetch when it is part of //packages/agent/dependencies:all_files
        "//deps/jmxfetch:all_files",
        "//packages/agent/dependencies:all_files",
        "//packages/install_dir:embedded",
        "//packages/install_dir:etc",
    ],
    prefix = "/opt/datadog-agent",
)

# This is the list of things we depend on transitively. It is temporary.
# They are really deps of things that the agent binary or python3 depends
# on. We should pick up the dependencies from the build graph.
# https://datadoghq.atlassian.net/browse/ABLD-363 will resolve that.
# This is good enough for today.
pkg_filegroup(
    name = "transitive_deps",
    srcs = [
        "//deps/gstatus:bin_files",
        "//deps/msodbcsql18:all_files",
        "//deps/nfsiostat:all_files",
        "//deps/openscap:all_files",
        "@freetds//:all_files",
        "@krb5//:all_files",
        "@nghttp2//:all_files",
        "@xz//:all_files",
    ],
    prefix = "/opt/datadog-agent",
)

# This is an intermediate node so that have a place to collect all
# the dependencies so we can walk the aspect to gather the OSS
# licenses.  This is close to "everything except for licenses"
# Do not include things that vary per build, like symlinks using
# the pipeline name.
pkg_filegroup(
    name = "everything",
    srcs = [
        ":agent_components",
        ":transitive_deps",
    ],
)

# Gather licenses for all the pieces of the distribuion
package_licenses(
    name = "license_files",
    src = ":everything",
)

# Blend the dependencies, the license files and things that vary
# a build time into the distribution.
pkg_filegroup(
    name = "whole_distro",
    srcs = [
        ":everything",
        ":license_files",
    ] + select({
        "//:is_standard_install": [
            ":datadog_agent_installer_symlinks",
        ],
        "//conditions:default": [],
    }),
)

pkg_tar(
    name = "whole_distro_tar",
    srcs = [":whole_distro"],
    extension = "tar.xz",
    # TODO: account for pipeline injected compression level.
    # https://datadoghq.atlassian.net/browse/ABLD-364
    # compression_threads COMPRESSION_THREADS
    # compression_level COMPRESSION_LEVEL
    owner = "0.0",
)

# TODO: Look at omnibus/config/projects/agent.rb for more content here.
pkg_deb(
    name = "debian",
    conflicts = ["datadog-iot-agent"],
    data = ":whole_distro_tar",
    description = """Datadog Monitoring Agent
The Datadog Monitoring Agent is a lightweight process that monitors system
processes and services, and sends information back to your Datadog account.
.
This package installs and runs the advanced Agent daemon, which queues and
forwards metrics from your applications as well as system services.
.
See http://www.datadoghq.com/ for more information
""",
    homepage = "http://www.datadoghq.com",
    # epoch 1
    # vendor = "Datadog <package@datadoghq.com>",
    license = "Apache License Version 2.0",
    maintainer = "Datadog Packages <package@datadoghq.com>",
    package = "datadog-agent",
    priority = "extra",
    recommends = ["datadog-signing-keys (>= 1:1.4.0)"],
    section = "utils",
    # TODO: Stamp in pipeline: 1:7.77.0~devel.git.394.fe0d0bb.pipeline.95313893-1
    version = "7",

    # TODO: pull in target_cpu as arch.
    # architecture = "$(COMPILATION_MODE)",
    # config = "config",
    #distribution = "trusty",
)

# Replacement for datadog-agent-installer-symlinks.rb

pkg_mkdirs(
    name = "dirs",
    attributes = pkg_attributes(mode = "755"),
    dirs = [
        "opt/datadog-packages",
        "opt/datadog-packages/datadog-agent",
        "opt/datadog-packages/run",
        "opt/datadog-packages/run/datadog-agent",
    ],
)

dd_agent_pkg_mklink(
    name = "experiment_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "/opt/datadog-packages/datadog-agent/experiment",
    target = "/opt/datadog-packages/datadog-agent/stable",
)

dd_agent_pkg_mklink(
    name = "stable_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "/opt/datadog-packages/datadog-agent/stable",
    target = "/opt/datadog-packages/run/datadog-agent/{build_version}",
)

dd_agent_pkg_mklink(
    name = "version_to_install_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "opt/datadog-packages/run/datadog-agent/{build_version}",
    target = "{install_dir}",
)

# note that this does not follow the naming pattern of "all_files".
# Instead, I'm using the name of the omnibus script it will replace.
# That provides a clearer meaning during the migration than the name
# of the package (packages/agent/linux) alone. The "all_files" in
# //deps/... does not have the problem because the intent is obvious
# from the path.
pkg_filegroup(
    name = "datadog_agent_installer_symlinks",
    srcs = [
        ":dirs",
        ":experiment_link",
        ":stable_link",
        ":version_to_install_link",
    ],
)

# NOTE: These are files that get installed outside of {install_dir}. During the
# migration we must account for them with extra_package_files in omnibus scripts
pkg_filegroup(
    name = "all_files",
    srcs = [
        "//pkg/privateactionrunner/autoconnections/conf:all_files",
    ] + select({
        "//:is_standard_install": [
            ":datadog_agent_installer_symlinks",
        ],
        "//conditions:default": [],
    }),
)

# This target is only for the omnibus install
pkg_install(
    name = "install",
    srcs = [":all_files"],
)
