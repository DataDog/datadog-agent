"""Agent package components specific to linux."""

load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("@rules_pkg//pkg:install.bzl", "pkg_install")
load(
    "@rules_pkg//pkg:mappings.bzl",
    "pkg_attributes",
    "pkg_filegroup",
    "pkg_mkdirs",
)
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//bazel/rules:dd_agent_pkg_mklink.bzl", "dd_agent_pkg_mklink")
load("//compliance:package_licenses.bzl", "package_licenses")

package(default_visibility = ["//packages:__subpackages__"])

# This is a placeholder for an eventual group containg (the future) top level binaries
# such as //cmd/agent:agent.  The individual items here will become dependencies of
# those targets.
filegroup(
    name = "all_deps",
    srcs = [
        "//deps:all_deps",
    ] + select({
        "@platforms//os:linux": [
            "//deps/nfsiostat:all_files",
            "//deps/openscap:all_deps",
        ],
        "//conditions:default": [],
    }),
)

package_licenses(
    name = "license_files",
    src = ":all_deps",
)

# This is good enough for today. It will go away as part of
# https://datadoghq.atlassian.net/browse/ABLD-363
pkg_filegroup(
    name = "whole_distro",
    srcs = [
        ":license_files",
        "//deps/msodbcsql18:all_files",
        "//deps/nfsiostat:all_files",
        "//deps/openscap:all_files",
        "//packages/install_dir:embedded",
        "//packages/install_dir:etc",
        "@freetds//:all_files",
        "@krb5//:all_files",
        "@nghttp2//:all_files",
    ],
    prefix = "/opt/datadog-agent",
)

pkg_tar(
    name = "whole_distro_tar",
    srcs = [":whole_distro"] + select({
        "//:is_standard_install": [
            ":datadog_agent_installer_symlinks",
        ],
        "//conditions:default": [],
    }),
    extension = "tar.xz",
    # TODO: account for pipeline injected compression level.
    # https://datadoghq.atlassian.net/browse/ABLD-364
    # compression_threads COMPRESSION_THREADS
    # compression_level COMPRESSION_LEVEL
    owner = "0.0",
)

# TODO: Look at omnibus/config/projects/agent.rb for more content here.
pkg_deb(
    name = "debian",
    data = ":whole_distro_tar",
    description = """Datadog Monitoring Agent
The Datadog Monitoring Agent is a lightweight process that monitors system
processes and services, and sends information back to your Datadog account.
.
This package installs and runs the advanced Agent daemon, which queues and
forwards metrics from your applications as well as system services.
.
See http://www.datadoghq.com/ for more information
""",
    # epoch 1
    # vendor = "Datadog <package@datadoghq.com>",
    license = "Apache License Version 2.0",
    maintainer = "Datadog Packages <package@datadoghq.com>",
    package = "datadog-agent",
    priority = "extra",
    section = "utils",
    version = "7",

    # This does not make sense for architecture, but for testing, compilation
    # mode is more stable than cpu.
    # architecture = "$(COMPILATION_MODE)",
    # config = "config",
    #distribution = "trusty",
)

# Replacement for datadog-agent-installer-symlinks.rb

pkg_mkdirs(
    name = "dirs",
    attributes = pkg_attributes(mode = "755"),
    dirs = [
        "opt/datadog-packages",
        "opt/datadog-packages/datadog-agent",
        "opt/datadog-packages/run",
        "opt/datadog-packages/run/datadog-agent",
    ],
)

dd_agent_pkg_mklink(
    name = "experiment_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "/opt/datadog-packages/datadog-agent/experiment",
    target = "/opt/datadog-packages/datadog-agent/stable",
)

# TODO: Figure out how to point to the build version
# stable -> /opt/datadog-packages/run/datadog-agent/7.75.0-devel.git.510.0eb2d89.pipeline.87895082
dd_agent_pkg_mklink(
    name = "stable_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "/opt/datadog-packages/datadog-agent/stable",
    target = "/opt/datadog-packages/run/datadog-agent/{build_version}",
    #link "/opt/datadog-agent", "/opt/datadog-packages/run/datadog-agent/{build_version}"
)

dd_agent_pkg_mklink(
    name = "version_to_install_link",
    attributes = pkg_attributes(mode = "755"),
    link_name = "opt/datadog-packages/run/datadog-agent/{build_version}",
    target = "{install_dir}",
)

# note that this does not follow the naming pattern of "all_files".
# Instead, I'm using the name of the omnibus script it will replace.
# That provides a clearer meaning during the migration than the name
# of the package (packages/agent/linux) alone.
pkg_filegroup(
    name = "datadog_agent_installer_symlinks",
    srcs = [
        ":dirs",
        ":experiment_link",
        ":stable_link",
        ":version_to_install_link",
    ],
)

pkg_install(
    name = "install",
    srcs = select({
        "//:is_standard_install": [
            ":datadog_agent_installer_symlinks",
        ],
        "//conditions:default": [],
    }),
)
